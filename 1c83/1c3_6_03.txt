Функция ПолучитьТаблицуДанныхПлана(ПараметрыОтчета, ПериодНачало, ПериодКонец,
	МассивПоказателейПлан, МассивОклад, МассивПремияЗаМесяц, МассивКвартальнаяПремия, МассивВыплатаПоРН, МассивВыплатаПоРК, МассивВыплатаПоСН)
	
	//расчет данных
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", 			ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПериодНачало", 			ПериодНачало);
	Запрос.УстановитьПараметр("ПериодКонец", 			ПериодКонец);
	Запрос.УстановитьПараметр("МассивПоказателей", 		МассивПоказателейПлан);
	Запрос.УстановитьПараметр("МассивОклад", 			МассивОклад);
	Запрос.УстановитьПараметр("МассивПремияЗаМесяц", 	МассивПремияЗаМесяц);
	Запрос.УстановитьПараметр("МассивКвартальнаяПремия",МассивКвартальнаяПремия);
	Запрос.УстановитьПараметр("МассивВыплатаПоРН", 		МассивВыплатаПоРН);
	Запрос.УстановитьПараметр("МассивВыплатаПоРК", 		МассивВыплатаПоРК);
	Запрос.УстановитьПараметр("МассивВыплатаПоСН", 		МассивВыплатаПоСН);
	
	СтатьяОсновнойДеятельности = Справочники.СтатьиФинансированияЗарплата.НайтиПоКоду("О" + СокрЛП(ПараметрыОтчета.Организация.Префикс));
	Запрос.УстановитьПараметр("СтатьяОсновнойДеятельности", СтатьяОсновнойДеятельности);
	
	//1. первая часть - определим действующие позиции по штатному расписанию
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания.Владелец КАК Организация,
	|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ИсторияИспользованияШтатногоРасписания.Дата КАК Дата,
	|	ИсторияИспользованияШтатногоРасписания.КоличествоСтавок КАК КоличествоСтавок,
	|	ИсторияИспользованияШтатногоРасписания.Используется КАК Используется
	|ПОМЕСТИТЬ ВТВыборка
	|ИЗ
	|	РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
	|ГДЕ
	|	ИсторияИспользованияШтатногоРасписания.Дата <= &ПериодНачало
	|	И ИсторияИспользованияШтатногоРасписания.Активность = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВыборка.Организация КАК Организация,
	|	ВТВыборка.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	МАКСИМУМ(ВТВыборка.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТСрезПоследних
	|ИЗ
	|	ВТВыборка КАК ВТВыборка
	|ГДЕ
	|	ВТВыборка.Организация В(&Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВыборка.Организация,
	|	ВТВыборка.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСрезПоследних.Организация КАК Организация,
	|	ВТСрезПоследних.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ЕСТЬNULL(ВТВыборка.КоличествоСтавок, 0) КАК КоличествоСтавок,
	|	ЕСТЬNULL(ВТВыборка.Используется, ЛОЖЬ) КАК Используется
	|ПОМЕСТИТЬ ВТПодИтог
	|ИЗ
	|	ВТСрезПоследних КАК ВТСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВыборка КАК ВТВыборка
	|		ПО ВТСрезПоследних.Организация = ВТВыборка.Организация
	|			И ВТСрезПоследних.ПозицияШтатногоРасписания = ВТВыборка.ПозицияШтатногоРасписания
	|			И ВТСрезПоследних.Дата = ВТВыборка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПодИтог.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	ВТПодИтог.КоличествоСтавок КАК КоличествоСтавок
	|ПОМЕСТИТЬ ВТДействующиеПозиции
	|ИЗ
	|	ВТПодИтог КАК ВТПодИтог
	|ГДЕ
	|	ВТПодИтог.КоличествоСтавок > 0
	|	И ВТПодИтог.Используется = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВыборка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПодИтог";
	
	Запрос.Выполнить();
	
	//2. вторая часть - определение показателей
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИсторияПоказателейПоШтатномуРасписанию.Дата,
	|	ИсторияПоказателейПоШтатномуРасписанию.ПозицияШтатногоРасписания,
	|	ИсторияПоказателейПоШтатномуРасписанию.Показатель,
	|	ИсторияПоказателейПоШтатномуРасписанию.Значение
	|ПОМЕСТИТЬ ВТВыборДанных
	|ИЗ
	|	РегистрСведений.ИсторияПоказателейПоШтатномуРасписанию КАК ИсторияПоказателейПоШтатномуРасписанию
	|ГДЕ
	|	ИсторияПоказателейПоШтатномуРасписанию.Активность = ИСТИНА
	|	И ИсторияПоказателейПоШтатномуРасписанию.Дата <= &ПериодНачало
	|	И ИсторияПоказателейПоШтатномуРасписанию.ПозицияШтатногоРасписания В
	|			(ВЫБРАТЬ
	|				ВТДействующиеПозиции.ПозицияШтатногоРасписания
	|			ИЗ
	|				ВТДействующиеПозиции)
	|	И ИсторияПоказателейПоШтатномуРасписанию.Показатель В(&МассивПоказателей)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВыборДанных.Дата) КАК Дата,
	|	ВТВыборДанных.ПозицияШтатногоРасписания
	|ПОМЕСТИТЬ ВТПоследнееЗначение
	|ИЗ
	|	ВТВыборДанных КАК ВТВыборДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВыборДанных.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПоследнееЗначение.ПозицияШтатногоРасписания,
	|	ВТВыборДанных.Показатель КАК Показатель,
	|	ВТВыборДанных.Значение КАК Значение
	|ПОМЕСТИТЬ ВТПоказатели
	|ИЗ
	|	ВТПоследнееЗначение КАК ВТПоследнееЗначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВыборДанных КАК ВТВыборДанных
	|		ПО ВТПоследнееЗначение.ПозицияШтатногоРасписания = ВТВыборДанных.ПозицияШтатногоРасписания
	|			И ВТПоследнееЗначение.Дата = ВТВыборДанных.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТПоследнееЗначение.ПозицияШтатногоРасписания,
	|	ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделенийСрезПоследних.Показатель,
	|	ЕСТЬNULL(ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделенийСрезПоследних.Значение, 0)
	|ИЗ
	|	ВТПоследнееЗначение КАК ВТПоследнееЗначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделений.СрезПоследних(
	|				&ПериодНачало,
	|				Организация = &Организация
	|					И Показатель В (&МассивВыплатаПоРН)) КАК ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделенийСрезПоследних
	|		ПО ВТПоследнееЗначение.ПозицияШтатногоРасписания.Подразделение = ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделенийСрезПоследних.Подразделение
	|ГДЕ
	|	ЕСТЬNULL(ЗначенияПериодическихПоказателейРасчетаЗарплатыПодразделенийСрезПоследних.Значение, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПоказатели.ПозицияШтатногоРасписания,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивОклад)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Оклад,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивПремияЗаМесяц)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПремияЗаМесяц,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивКвартальнаяПремия)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КвартальнаяПремия,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивВыплатаПоРН)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыплатаПоРН,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивВыплатаПоРК)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыплатаПоРК,
	|	ВЫБОР
	|		КОГДА ВТПоказатели.Показатель В (&МассивВыплатаПоСН)
	|			ТОГДА ВТПоказатели.Значение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыплатаПоСН
	|ПОМЕСТИТЬ ВТРаспределение
	|ИЗ
	|	ВТПоказатели КАК ВТПоказатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРаспределение.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
	|	СУММА(ВТРаспределение.Оклад) КАК Оклад,
	|	СУММА(ВТРаспределение.ПремияЗаМесяц) КАК ПремияЗаМесяц,
	|	СУММА(ВТРаспределение.КвартальнаяПремия) КАК КвартальнаяПремия,
	|	СУММА(ВТРаспределение.ВыплатаПоРН) КАК ВыплатаПоРН
	|ПОМЕСТИТЬ ВТПоказателиИтог
	|ИЗ
	|	ВТРаспределение КАК ВТРаспределение
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТРаспределение.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВыборДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПоследнееЗначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПоказатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТРаспределение";
	
	Запрос.Выполнить();
	
	//3. третья часть - определение проекта
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КадроваяИсторияСотрудниковСрезПоследних.ДолжностьПоШтатномуРасписанию КАК ПозицияШтатногоРасписания,
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник
	|ПОМЕСТИТЬ ВТЗанятыеПозиции
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&ПериодКонец, Организация = &Организация) КАК КадроваяИсторияСотрудниковСрезПоследних
	|ГДЕ
	|	КадроваяИсторияСотрудниковСрезПоследних.ДолжностьПоШтатномуРасписанию В
	|			(ВЫБРАТЬ
	|				ВТДействующиеПозиции.ПозицияШтатногоРасписания
	|			ИЗ
	|				ВТДействующиеПозиции)
	|	И НЕ КадроваяИсторияСотрудниковСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетРаспределениеОсновногоЗаработка.Сотрудник,
	|	БухучетРаспределениеОсновногоЗаработка.АФМ_ПериодНачало,
	|	БухучетРаспределениеОсновногоЗаработка.СтатьяФинансирования
	|ПОМЕСТИТЬ ВТБухучет
	|ИЗ
	|	РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК БухучетРаспределениеОсновногоЗаработка
	|ГДЕ
	|	БухучетРаспределениеОсновногоЗаработка.Организация = &Организация
	|	И БухучетРаспределениеОсновногоЗаработка.Активность = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучет.Сотрудник,
	|	МАКСИМУМ(ВТБухучет.АФМ_ПериодНачало) КАК АФМ_ПериодНачало,
	|	ИСТИНА КАК Отбор
	|ПОМЕСТИТЬ ВТБухучетСрезПоследних
	|ИЗ
	|	ВТБухучет КАК ВТБухучет
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТБухучет.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучет.Сотрудник,
	|	ВТБухучет.СтатьяФинансирования
	|ПОМЕСТИТЬ ВТБухучетИтог
	|ИЗ
	|	ВТБухучет КАК ВТБухучет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетСрезПоследних КАК ВТБухучетСрезПоследних
	|		ПО ВТБухучет.Сотрудник = ВТБухучетСрезПоследних.Сотрудник
	|			И ВТБухучет.АФМ_ПериодНачало = ВТБухучетСрезПоследних.АФМ_ПериодНачало
	|ГДЕ
	|	ЕСТЬNULL(ВТБухучетСрезПоследних.Отбор, ЛОЖЬ) = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗанятыеПозиции.ПозицияШтатногоРасписания,
	|	ЕСТЬNULL(ВТБухучетИтог.СтатьяФинансирования, &СтатьяОсновнойДеятельности) КАК СтатьяФинансирования,
	|	1 КАК ДоляСотрудника
	|ПОМЕСТИТЬ ВТОбщая
	|ИЗ
	|	ВТЗанятыеПозиции КАК ВТЗанятыеПозиции
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетИтог КАК ВТБухучетИтог
	|		ПО ВТЗанятыеПозиции.Сотрудник = ВТБухучетИтог.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбщая.ПозицияШтатногоРасписания,
	|	СУММА(ВТОбщая.ДоляСотрудника) КАК ЕденицВсего
	|ПОМЕСТИТЬ ВТИтог1
	|ИЗ
	|	ВТОбщая КАК ВТОбщая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОбщая.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбщая.ПозицияШтатногоРасписания,
	|	ВТОбщая.СтатьяФинансирования,
	|	СУММА(ВТОбщая.ДоляСотрудника) КАК ДоляСотрудника
	|ПОМЕСТИТЬ ВТИтог2
	|ИЗ
	|	ВТОбщая КАК ВТОбщая
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОбщая.ПозицияШтатногоРасписания,
	|	ВТОбщая.СтатьяФинансирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИтог2.ПозицияШтатногоРасписания,
	|	ВТИтог2.СтатьяФинансирования,
	|	ВТИтог2.ДоляСотрудника,
	|	ВТИтог1.ЕденицВсего КАК ЕденицВсего
	|ПОМЕСТИТЬ ВТСтатьиФинансирования
	|ИЗ
	|	ВТИтог2 КАК ВТИтог2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтог1 КАК ВТИтог1
	|		ПО ВТИтог2.ПозицияШтатногоРасписания = ВТИтог1.ПозицияШтатногоРасписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТЗанятыеПозиции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетИтог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОбщая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТИтог1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТИтог2";
	
	Запрос.Выполнить();
	
	//4. четвертая часть - расчет сумм
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТПоказателиИтог.ПозицияШтатногоРасписания,
	|	ЕСТЬNULL(ВТДействующиеПозиции.КоличествоСтавок, 0) КАК КоличествоСтавок,
	|	ВТПоказателиИтог.Оклад,
	|	ВТПоказателиИтог.ПремияЗаМесяц,
	|	ВТПоказателиИтог.КвартальнаяПремия,
	|	ВТПоказателиИтог.ВыплатаПоРН,
	|	ЕСТЬNULL(ВТСтатьиФинансирования.СтатьяФинансирования, &СтатьяОсновнойДеятельности) КАК Проект,
	|	ЕСТЬNULL(ВТСтатьиФинансирования.ДоляСотрудника, 1) КАК ДоляСотрудника,
	|	ЕСТЬNULL(ВТСтатьиФинансирования.ЕденицВсего, 1) КАК ЕденицВсего
	|ИЗ
	|	ВТПоказателиИтог КАК ВТПоказателиИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДействующиеПозиции КАК ВТДействующиеПозиции
	|		ПО ВТПоказателиИтог.ПозицияШтатногоРасписания = ВТДействующиеПозиции.ПозицияШтатногоРасписания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиФинансирования КАК ВТСтатьиФинансирования
	|		ПО ВТПоказателиИтог.ПозицияШтатногоРасписания = ВТСтатьиФинансирования.ПозицияШтатногоРасписания";
	
	ТаблицаДанные = Запрос.Выполнить().Выгрузить();
	
	//отбор по проекту,
	//	деление сумм на проекты при расчете строки.
	Если ПараметрыОтчета.ВключитьОтборПоПроекту = Истина Тогда
		
		МассивУдаляемыхСтрок = Новый Массив;
		Для Каждого СтрокаДанных Из ТаблицаДанные Цикл
			Если НЕ СтрокаДанных.Проект = ПараметрыОтчета.Проект Тогда
				МассивУдаляемыхСтрок.Добавить(СтрокаДанных);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
			ТаблицаДанные.Удалить(УдаляемаяСтрока);
		КонецЦикла;
		
	КонецЕсли;
	
	//завершение
	Возврат ТаблицаДанные;
	
КонецФункции



Процедура РасчетФакта(ПараметрыОтчета, ПараметрыРасчет, МассивПоказателей, МассивПремияЗаМесяц, МассивКвартальнаяПремия, МассивВыплатаПоРН, МассивВыплатаПоРК, МассивВыплатаПоСН)
	
	Для НомерКвартала = 1 По 4 Цикл
		
		//параметры расчета
		ПериодНачало = Дата(Год(ПараметрыОтчета.НачПериод),(1+(НомерКвартала-1)*3),1,0,0,0);
		ПериодКонец = КонецКвартала(ПериодНачало);
		
		//расчет данных
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", 			ПараметрыОтчета.Организация);
		Запрос.УстановитьПараметр("ПериодНачало", 			ПериодНачало);
		Запрос.УстановитьПараметр("ПериодКонец", 			ПериодКонец);
		Запрос.УстановитьПараметр("МассивПремияЗаМесяц", 	МассивПремияЗаМесяц);
		Запрос.УстановитьПараметр("МассивКвартальнаяПремия",МассивКвартальнаяПремия);
		Запрос.УстановитьПараметр("МассивВыплатаПоРН", 		МассивВыплатаПоРН);
		Запрос.УстановитьПараметр("МассивВыплатаПоРК", 		МассивВыплатаПоРК);
		Запрос.УстановитьПараметр("МассивВыплатаПоСН", 		МассивВыплатаПоСН);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияПоказатели.Ссылка КАК ВидРасчета
		|ПОМЕСТИТЬ ВТПремияЗаМесяц
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|ГДЕ
		|	НачисленияПоказатели.Показатель В(&МассивПремияЗаМесяц)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПоказатели.Ссылка КАК ВидРасчета
		|ПОМЕСТИТЬ ВТКвартальнаяПремия
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|ГДЕ
		|	НачисленияПоказатели.Показатель В(&МассивКвартальнаяПремия)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПоказатели.Ссылка КАК ВидРасчета
		|ПОМЕСТИТЬ ВТВыплатаПоРН
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|ГДЕ
		|	НачисленияПоказатели.Показатель В(&МассивВыплатаПоРН)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПоказатели.Ссылка КАК ВидРасчета
		|ПОМЕСТИТЬ ВТВыплатаПоРК
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|ГДЕ
		|	НачисленияПоказатели.Показатель В(&МассивВыплатаПоРК)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияПоказатели.Ссылка КАК ВидРасчета
		|ПОМЕСТИТЬ ВТВыплатаПоСН
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|ГДЕ
		|	НачисленияПоказатели.Показатель В(&МассивВыплатаПоСН)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПремияЗаМесяц.ВидРасчета
		|ПОМЕСТИТЬ ВТВидыРасчета
		|ИЗ
		|	ВТПремияЗаМесяц КАК ВТПремияЗаМесяц
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТКвартальнаяПремия.ВидРасчета
		|ИЗ
		|	ВТКвартальнаяПремия КАК ВТКвартальнаяПремия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТВыплатаПоРН.ВидРасчета
		|ИЗ
		|	ВТВыплатаПоРН КАК ВТВыплатаПоРН
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТВыплатаПоРК.ВидРасчета
		|ИЗ
		|	ВТВыплатаПоРК КАК ВТВыплатаПоРК
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТВыплатаПоСН.ВидРасчета
		|ИЗ
		|	ВТВыплатаПоСН КАК ВТВыплатаПоСН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияУдержанияПоСотрудникамОбороты.Организация,
		|	НачисленияУдержанияПоСотрудникамОбороты.НачислениеУдержание,
		|	СУММА(НачисленияУдержанияПоСотрудникамОбороты.СуммаОборот) КАК СуммаОборот,
		|	НачисленияУдержанияПоСотрудникамОбороты.СтатьяФинансирования КАК Проект
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам.Обороты(
		|			&ПериодНачало,
		|			&ПериодКонец,
		|			,
		|			Организация = &Организация
		|				И НачислениеУдержание В
		|					(ВЫБРАТЬ
		|						ВТВидыРасчета.ВидРасчета
		|					ИЗ
		|						ВТВидыРасчета)) КАК НачисленияУдержанияПоСотрудникамОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	НачисленияУдержанияПоСотрудникамОбороты.Организация,
		|	НачисленияУдержанияПоСотрудникамОбороты.НачислениеУдержание,
		|	НачисленияУдержанияПоСотрудникамОбороты.СтатьяФинансирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНачисления.Организация,
		|	ВЫБОР
		|		КОГДА ВТНачисления.НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					ВТПремияЗаМесяц.ВидРасчета
		|				ИЗ
		|					ВТПремияЗаМесяц)
		|			ТОГДА ВТНачисления.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПремияЗаМесяц,
		|	ВЫБОР
		|		КОГДА ВТНачисления.НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					ВТКвартальнаяПремия.ВидРасчета
		|				ИЗ
		|					ВТКвартальнаяПремия)
		|			ТОГДА ВТНачисления.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КвартальнаяПремия,
		|	ВЫБОР
		|		КОГДА ВТНачисления.НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					ВТВыплатаПоРН.ВидРасчета
		|				ИЗ
		|					ВТВыплатаПоРН)
		|			ТОГДА ВТНачисления.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВыплатаПоРН,
		|	ВЫБОР
		|		КОГДА ВТНачисления.НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					ВТВыплатаПоРК.ВидРасчета
		|				ИЗ
		|					ВТВыплатаПоРК)
		|			ТОГДА ВТНачисления.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВыплатаПоРК,
		|	ВЫБОР
		|		КОГДА ВТНачисления.НачислениеУдержание В
		|				(ВЫБРАТЬ
		|					ВТВыплатаПоСН.ВидРасчета
		|				ИЗ
		|					ВТВыплатаПоСН)
		|			ТОГДА ВТНачисления.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВыплатаПоСН,
		|	ВТНачисления.СуммаОборот КАК Сумма,
		|	ВТНачисления.Проект
		|ПОМЕСТИТЬ ВТНачисленияСРазбивкой
		|ИЗ
		|	ВТНачисления КАК ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНачисленияСРазбивкой.Организация,
		|	ВТНачисленияСРазбивкой.Проект,
		|	СУММА(ВТНачисленияСРазбивкой.ПремияЗаМесяц) КАК ПремияЗаМесяц,
		|	СУММА(ВТНачисленияСРазбивкой.КвартальнаяПремия) КАК КвартальнаяПремия,
		|	СУММА(ВТНачисленияСРазбивкой.ВыплатаПоРН) КАК ВыплатаПоРН,
		|	СУММА(ВТНачисленияСРазбивкой.ВыплатаПоРК) КАК ВыплатаПоРК,
		|	СУММА(ВТНачисленияСРазбивкой.ВыплатаПоСН) КАК ВыплатаПоСН,
		|	СУММА(ВТНачисленияСРазбивкой.Сумма) КАК Сумма
		|ИЗ
		|	ВТНачисленияСРазбивкой КАК ВТНачисленияСРазбивкой
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТНачисленияСРазбивкой.Организация,
		|	ВТНачисленияСРазбивкой.Проект";
		
		ТаблицаДанные = Запрос.Выполнить().Выгрузить();
		
		//отбор по проекту
		Если ПараметрыОтчета.ВключитьОтборПоПроекту = Истина Тогда
			
			МассивУдаляемыхСтрок = Новый Массив;
			Для Каждого СтрокаДанных Из ТаблицаДанные Цикл
				Если НЕ СтрокаДанных.Проект = ПараметрыОтчета.Проект Тогда
					МассивУдаляемыхСтрок.Добавить(СтрокаДанных);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
				ТаблицаДанные.Удалить(УдаляемаяСтрока);
			КонецЦикла;
			
		КонецЕсли;
		
		ТаблицаДанные.Свернуть("Организация", "ПремияЗаМесяц,КвартальнаяПремия,ВыплатаПоРН,ВыплатаПоРК,ВыплатаПоСН,Сумма");
		Если ТаблицаДанные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		//запись в параметры расчета
		ПараметрыРасчет.Вставить("Расход2_" + СокрЛП(НомерКвартала), ТаблицаДанные[0].ПремияЗаМесяц);
		ПараметрыРасчет.Вставить("Расход3_" + СокрЛП(НомерКвартала), ТаблицаДанные[0].КвартальнаяПремия);
		ПараметрыРасчет.Вставить("Расход4_" + СокрЛП(НомерКвартала), ТаблицаДанные[0].ВыплатаПоРН);
		ПараметрыРасчет.Вставить("Расход5_" + СокрЛП(НомерКвартала), ТаблицаДанные[0].ВыплатаПоРК);
		ПараметрыРасчет.Вставить("Расход6_" + СокрЛП(НомерКвартала), ТаблицаДанные[0].ВыплатаПоСН);
		
		ОбщаяСумма = ТаблицаДанные[0].ПремияЗаМесяц + ТаблицаДанные[0].КвартальнаяПремия + ТаблицаДанные[0].ВыплатаПоРН + ТаблицаДанные[0].ВыплатаПоРК + ТаблицаДанные[0].ВыплатаПоСН;
		ПараметрыРасчет.Вставить("Расход7_" + СокрЛП(НомерКвартала), Окр(ОбщаяСумма * ПараметрыОтчета.СтавкаПФР, 2));
		ПараметрыРасчет.Вставить("Расход8_" + СокрЛП(НомерКвартала), Окр(ОбщаяСумма * ПараметрыОтчета.СтавкаФССНС, 2));
		
		СуммаПоФилиалу = 0;
		Для НомерСтроки = 2 По 8 Цикл
			СуммаПоФилиалу = СуммаПоФилиалу + ПараметрыРасчет["Расход" + СокрЛП(НомерСтроки) + "_" + СокрЛП(НомерКвартала)];
		КонецЦикла;
		ПараметрыРасчет.Вставить("Расход1_" + СокрЛП(НомерКвартала), СуммаПоФилиалу);
		
	КонецЦикла;
	
КонецПроцедуры
