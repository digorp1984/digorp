&НаСервере
Процедура ЗагрузитьСервер()
	
	//параметры
	ИмяМакета=СокрЛП(Объект.МакетЗагрузки);
	Макет=РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета);
	
	Организация=Справочники.Организации.ПустаяСсылка();
	Для Каждого СтрСоответсвие Из ТаблицаМакетОрганизация Цикл
		Если СтрСоответсвие.ИмяМакета=ИмяМакета Тогда
			Организация=СтрСоответсвие.Организация;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ТблДанные=Новый ТаблицаЗначений;
	ТблДанные.Колонки.Добавить("УжеЕстьДанные");
	ТблДанные.Колонки.Добавить("ГоловнаяОрганизация");
	ТблДанные.Колонки.Добавить("ФизическоеЛицо");
	ТблДанные.Колонки.Добавить("ФИО");
	ТблДанные.Колонки.Добавить("ТабельныйНомер");
	ТблДанные.Колонки.Добавить("МесяцНалоговогоПериода");
	ТблДанные.Колонки.Добавить("МесяцНалоговогоПериодаТекст");
	ТблДанные.Колонки.Добавить("РегистрацияВНалоговомОргане");
	ТблДанные.Колонки.Добавить("КПП");
	ТблДанные.Колонки.Добавить("Организация");
	ТблДанные.Колонки.Добавить("РеквизитыПлатежногоПоручения");
	ТблДанные.Колонки.Добавить("ВидОплаты");
	
	//чтение данных
	Для СтрокаМакета=2 По Макет.ВысотаТаблицы Цикл
		
		ТабельныйНомер=СокрЛП(Макет.Область(СтрокаМакета, 1).Текст);
		РеквизитыПлатежногоПоручения=СокрЛП(Макет.Область(СтрокаМакета, 5).Текст);
		
		Если ТабельныйНомер="" ИЛИ РеквизитыПлатежногоПоручения="" Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДанных=ТблДанные.Добавить();
		СтрокаДанных.УжеЕстьДанные =				Ложь;
		СтрокаДанных.ГоловнаяОрганизация =			Организация;
		СтрокаДанных.ФизическоеЛицо =				Справочники.ФизическиеЛица.ПустаяСсылка();
		СтрокаДанных.ФИО =							СокрЛП(Макет.Область(СтрокаМакета, 2).Текст);
		СтрокаДанных.ТабельныйНомер =				ТабельныйНомер;
		СтрокаДанных.МесяцНалоговогоПериода =		Дата(1,1,1);
		СтрокаДанных.МесяцНалоговогоПериодаТекст =	СокрЛП(Макет.Область(СтрокаМакета, 3).Текст);
		СтрокаДанных.РегистрацияВНалоговомОргане =	Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
		СтрокаДанных.КПП =							Прав(СокрЛП(Макет.Область(СтрокаМакета, 4).Текст), 9);
		СтрокаДанных.Организация =					Организация;
		СтрокаДанных.РеквизитыПлатежногоПоручения =	РеквизитыПлатежногоПоручения;
		СтрокаДанных.ВидОплаты =					СокрЛП(Макет.Область(СтрокаМакета, 5).Текст);
		
	КонецЦикла;
	
	Если ТблДанные.Количество()=0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ОШИБКА: Таблица с данными пустая!");
	КонецЕсли;
	
	//определяем ссылки
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КПП
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.ПометкаУдаления = ЛОЖЬ
	|	И РегистрацииВНалоговомОргане.Владелец = &Организация";
	ТаблицаРегистраторов=Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.ГоловнаяОрганизация,
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.ФизическоеЛицо,
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.МесяцНалоговогоПериода,
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.РегистрацияВНалоговомОргане,
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.Организация,
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.РеквизитыПлатежногоПоручения
	|ИЗ
	|	РегистрСведений.АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП КАК АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП
	|ГДЕ
	|	АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.Организация = &Организация";
	ТаблицаДанныхВРегистре=Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДанных Из ТблДанные Цикл
		
		//общие
		СтрокаДанных.ФизическоеЛицо=Справочники.ФизическиеЛица.НайтиПоКоду(СтрокаДанных.ТабельныйНомер);
		
		//месяц
		Если НЕ СокрЛП(СтрокаДанных.МесяцНалоговогоПериодаТекст) = "" Тогда
			Попытка
				МесяцНалоговогоПериодаТекст = СтрокаДанных.МесяцНалоговогоПериодаТекст;
				МесяцНалоговогоПериодаГод = Число(Прав(МесяцНалоговогоПериодаТекст, 4));
				МесяцНалоговогоПериодаМесяц = Число(Сред(МесяцНалоговогоПериодаТекст, 4, 2));
				МесяцНалоговогоПериодаДень = Число(Лев(МесяцНалоговогоПериодаТекст, 2));
				МесяцНалоговогоПериода = Дата(МесяцНалоговогоПериодаГод, МесяцНалоговогоПериодаМесяц, МесяцНалоговогоПериодаДень);
				СтрокаДанных.МесяцНалоговогоПериода = МесяцНалоговогоПериода;
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		//регистрация в налоговом органе
		СтруктураПоиска=Новый Структура();
		СтруктураПоиска.Вставить("КПП", СтрокаДанных.КПП);
		НайденныеСтроки=ТаблицаРегистраторов.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаДанных.РегистрацияВНалоговомОргане=НайденныеСтроки[0].РегистрацияВНалоговомОргане;
		КонецЕсли;
		
		//наличие данных
		СтруктураПоиска=Новый Структура();
		СтруктураПоиска.Вставить("ГоловнаяОрганизация", СтрокаДанных.ГоловнаяОрганизация);
		СтруктураПоиска.Вставить("ФизическоеЛицо", СтрокаДанных.ФизическоеЛицо);
		СтруктураПоиска.Вставить("МесяцНалоговогоПериода", СтрокаДанных.МесяцНалоговогоПериода);
		СтруктураПоиска.Вставить("РегистрацияВНалоговомОргане", СтрокаДанных.РегистрацияВНалоговомОргане);
		СтруктураПоиска.Вставить("Организация", СтрокаДанных.Организация);
		НайденныеСтроки=ТаблицаДанныхВРегистре.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаДанных.УжеЕстьДанные=Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	//загрузка
	КолвоОбработанныхСтрок=0;
	Для Каждого СтрокаДанных Из ТблДанные Цикл
		//проверки
		Если СтрокаДанных.ФизическоеЛицо=Справочники.ФизическиеЛица.ПустаяСсылка()
			ИЛИ СтрокаДанных.РегистрацияВНалоговомОргане=Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка()
			ИЛИ СтрокаДанных.УжеЕстьДанные=Истина Тогда
			Продолжить;
		КонецЕсли;
		
		//записываем данные
		МенеджерЗаписи = РегистрыСведений.АФМ_РасчетыНалоговыхАгентовСБюджетомПоНДФЛРеквизитыПП.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ГоловнаяОрганизация = СтрокаДанных.ГоловнаяОрганизация;
		МенеджерЗаписи.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
		МенеджерЗаписи.МесяцНалоговогоПериода = СтрокаДанных.МесяцНалоговогоПериода;
		МенеджерЗаписи.РегистрацияВНалоговомОргане = СтрокаДанных.РегистрацияВНалоговомОргане;
		МенеджерЗаписи.Организация = СтрокаДанных.Организация;
		МенеджерЗаписи.Прочитать();
		
		Если НЕ МенеджерЗаписи.Выбран() Тогда
			
			МенеджерЗаписи.ГоловнаяОрганизация = СтрокаДанных.ГоловнаяОрганизация;
			МенеджерЗаписи.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
			МенеджерЗаписи.МесяцНалоговогоПериода = СтрокаДанных.МесяцНалоговогоПериода;
			МенеджерЗаписи.РегистрацияВНалоговомОргане = СтрокаДанных.РегистрацияВНалоговомОргане;
			МенеджерЗаписи.Организация = СтрокаДанных.Организация;
			МенеджерЗаписи.РеквизитыПлатежногоПоручения = СокрЛП(СтрокаДанных.РеквизитыПлатежногоПоручения);
			
			Попытка
				МенеджерЗаписи.Записать();
				КолвоОбработанныхСтрок = КолвоОбработанныхСтрок + 1;
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//завершение
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Загрузка ["+ИмяМакета+"] завершена успешно! Обработано объектов: "+СокрЛП(КолвоОбработанныхСтрок));
	
КонецПроцедуры
