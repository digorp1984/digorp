&НаСервере
Процедура СвернутьПроводки()
	
	//параметры
	МассивДокументов = Новый Массив;
	Для Каждого СтрокаДокумента Из Объект.Документы Цикл
		МассивДокументов.Добавить(СтрокаДокумента.ДокументСсылка);
	КонецЦикла;
	
	//выбор данных
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ак_ФормированиеПроводокСвернутыеПроводки.Ссылка КАК ДокументСсылка,
	|	ак_ФормированиеПроводокСвернутыеПроводки.СчетДебета,
	|	ак_ФормированиеПроводокСвернутыеПроводки.СчетКредита,
	|	ак_ФормированиеПроводокСвернутыеПроводки.Сумма,
	|	ак_ФормированиеПроводокСвернутыеПроводки.Содержание
	|ПОМЕСТИТЬ ВТВыборДанных
	|ИЗ
	|	Документ.ак_ФормированиеПроводок.СвернутыеПроводки КАК ак_ФормированиеПроводокСвернутыеПроводки
	|ГДЕ
	|	ак_ФормированиеПроводокСвернутыеПроводки.Ссылка В(&МассивДокументов)
	|	И ак_ФормированиеПроводокСвернутыеПроводки.ОФК_ВыгружалиРанее = ЛОЖЬ
	|	И ак_ФормированиеПроводокСвернутыеПроводки.Ссылка.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВыборДанных.ДокументСсылка КАК ДокументСсылка,
	|	СУММА(ВТВыборДанных.Сумма) КАК СуммаПоДокументу
	|ИЗ
	|	ВТВыборДанных КАК ВТВыборДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВыборДанных.ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВыборДанных.СчетДебета,
	|	ВТВыборДанных.СчетКредита,
	|	СУММА(ВТВыборДанных.Сумма) КАК Сумма,
	|	ВТВыборДанных.Содержание КАК Содержание
	|ИЗ
	|	ВТВыборДанных КАК ВТВыборДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВыборДанных.СчетДебета,
	|	ВТВыборДанных.СчетКредита,
	|	ВТВыборДанных.Содержание
	|
	|УПОРЯДОЧИТЬ ПО
	|	Содержание";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаДокументы = РезультатЗапроса[1].Выгрузить();
	ТаблицаСвернутыеПроводки = РезультатЗапроса[2].Выгрузить();
	
	//заполнение данных в объекте
	Объект.Документы.Очистить();
	Для Каждого СтроТаблицы Из ТаблицаДокументы Цикл
		НоваяСтрока = Объект.Документы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтроТаблицы);
	КонецЦикла;
	
	Объект.СвернутыеПроводки.Очистить();
	Для Каждого СтроТаблицы Из ТаблицаСвернутыеПроводки Цикл
		НоваяСтрока = Объект.СвернутыеПроводки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтроТаблицы);
	КонецЦикла;
	
КонецПроцедуры
