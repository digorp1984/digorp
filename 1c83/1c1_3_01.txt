Процедура ОбновлениеСпискаЗаявок(ОчиститьСмету=Истина)
	
	//Проверка ошибок
	ОчиститьСообщения();
	Ошибка=Ложь;
	Если Учреждение.Пустая()=Истина Тогда
		Ошибка=Истина;
		Сообщить("Не заполнено Учреждение");
	КонецЕсли; 
	Если Подразделение.Пустая()=Истина Тогда
		Ошибка=Истина;
		Сообщить("Не заполнено Подразделение");
	КонецЕсли;
	Если КВД.Пустая()=Истина Тогда
		Ошибка=Истина;
		Сообщить("Не заполнено КВД");
	КонецЕсли;
	Если Баланс.Пустая()=Истина Тогда
		Ошибка=Истина;
		Сообщить("Не заполнен Баланс");
	КонецЕсли;
	Если ПериодБюджета.Пустая() Тогда
		Ошибка=Истина;
		Сообщить("Не заполнен Период бюджета");
	КонецЕсли;
	Если Ошибка=Истина Тогда
		Возврат;
	КонецЕсли; 
	
	//Построение дерева
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	НоменклатураПринятаяПЭО.НоменклатураПЭО.Родитель КАК Родитель,
	             |	НоменклатураПринятаяПЭО.НоменклатураПЭО КАК НоменклатураПЭО,
	             |	НоменклатураПринятаяПЭО.Сумма,
	             |	НоменклатураПринятаяПЭО.Заявка КАК Заявка,
	             |	НоменклатураПринятаяПЭО.Заявка.Номер КАК Номер,
	             |	НоменклатураПринятаяПЭО.Заявка.Дата КАК Дата,
	             |	НоменклатураПринятаяПЭО.Заявка.ВнутреннийНомер КАК ВнутрНомер
	             |ПОМЕСТИТЬ Заявки
	             |ИЗ
	             |	РегистрСведений.НоменклатураПринятаяПЭО КАК НоменклатураПринятаяПЭО
	             |ГДЕ
	             |	НоменклатураПринятаяПЭО.Учреждение = &Учреждение
	             |	И НоменклатураПринятаяПЭО.Подразделение = &Подразделение
	             |	И НоменклатураПринятаяПЭО.КВД = &КВД
	             |	И НоменклатураПринятаяПЭО.Баланс = &Баланс
	             |	И НоменклатураПринятаяПЭО.ПериодБюджета = &ПериодБюджета
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	НоменклатураСметаПЭО.НоменклатураПЭО,
	             |	НоменклатураСметаПЭО.Заявка,
	             |	ИСТИНА КАК Признак
	             |ПОМЕСТИТЬ Смета
	             |ИЗ
	             |	РегистрСведений.НоменклатураСметаПЭО КАК НоменклатураСметаПЭО
	             |ГДЕ
	             |	НоменклатураСметаПЭО.Учреждение = &Учреждение
	             |	И НоменклатураСметаПЭО.Подразделение = &Подразделение
	             |	И НоменклатураСметаПЭО.КВД = &КВД
	             |	И НоменклатураСметаПЭО.Баланс = &Баланс
	             |	И НоменклатураСметаПЭО.ПериодБюджета = &ПериодБюджета
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	Заявки.Родитель КАК Родитель,
	             |	Заявки.НоменклатураПЭО КАК НоменклатураПЭО,
	             |	Заявки.Сумма,
	             |	Заявки.Заявка КАК Заявка,
	             |	Заявки.Номер,
	             |	Заявки.Дата,
	             |	Заявки.ВнутрНомер,
	             |	ЕСТЬNULL(Смета.Признак, ЛОЖЬ) КАК ПризнакВыбора
	             |ИЗ
	             |	Заявки КАК Заявки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ Смета КАК Смета
	             |		ПО Заявки.НоменклатураПЭО = Смета.НоменклатураПЭО
	             |			И Заявки.Заявка = Смета.Заявка
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	Родитель,
	             |	НоменклатураПЭО,
	             |	Заявка";
	Запрос.УстановитьПараметр("Учреждение", Учреждение);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("КВД", КВД);
	Запрос.УстановитьПараметр("Баланс", Баланс);
	Запрос.УстановитьПараметр("ПериодБюджета", ПериодБюджета);
	Выборка=Запрос.Выполнить().Выбрать();
	
	Дерево.Строки.Очистить();
	ТекУзел1=Неопределено;
	ТекУзел2=Неопределено;
	Пока Выборка.Следующий() Цикл
		Если НЕ ТекУзел1=Выборка.Родитель Тогда
			ТекУзел1=Выборка.Родитель;
			Узел1=Дерево.Строки.Добавить();
			Узел1.Вид=?(Выборка.НоменклатураПЭО=Справочники.НоменклатураПЭО.Командировка, "Командировки", Строка(Выборка.Родитель));
			Узел1.Заявка=Документы.ЗаявкиПодразделений.ПустаяСсылка();
			Узел1.Группа=1;
			Узел1.Выбор=Ложь;
			Узел1.Забито=Истина;
		КонецЕсли;
		Если НЕ ТекУзел2=Выборка.НоменклатураПЭО Тогда
			ТекУзел2=Выборка.НоменклатураПЭО;
			Узел2=Узел1.Строки.Добавить();
			Узел2.Вид=Выборка.НоменклатураПЭО;
			Узел2.Заявка=Документы.ЗаявкиПодразделений.ПустаяСсылка();
			Узел2.Группа=2;
			Узел2.Выбор=Ложь;
			Узел2.Забито=Истина;
		КонецЕсли;
		Узел3=Узел2.Строки.Добавить();
		Узел3.Вид="Заявка №"+Выборка.Номер+" от "+Формат(Выборка.Дата, "ДФ=dd.MM.yyyy")+" ("+Выборка.ВнутрНомер+")";		
		Узел3.Заявка=Выборка.Заявка;
		Узел3.Группа=3;
		Узел3.Выбор=Выборка.ПризнакВыбора;
		Узел3.Забито=Выборка.ПризнакВыбора;
		Узел3.НоменклатураПЭО=ТекУзел2;
		Узел3.Сумма=Выборка.Сумма;
	КонецЦикла;
	
	//Очищаем табличную часть
	Если ОчиститьСмету=Истина Тогда
		Смета.Очистить();
	КонецЕсли; 
	
	//Установим уже выбранные заявки в Смете
	Для каждого СтрокаСметы Из Смета Цикл
		РекурсияПоиска(Дерево, СтрокаСметы.Заявка, СтрокаСметы.НоменклатураПЭО, Истина);
	КонецЦикла; 
	
КонецПроцедуры
