Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();				   
		
	Попытка
		
		СтандартнаяОбработка = Ложь;
		
		//параметры отчета
		НачПериод = Дата(1,1,1);
		КонПериод = Дата(1,1,1);
		Параметр = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета"));
		Если ЗначениеЗаполнено(Параметр.Значение) Тогда
			НачПериод = Параметр.Значение.ДатаНачала;
			КонПериод = Параметр.Значение.ДатаОкончания;
		КонецЕсли;
		
		Организация = Справочники.Организации.ПустаяСсылка();
		Параметр = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
		Если ЗначениеЗаполнено(Параметр.Значение) Тогда
			Организация = Параметр.Значение;
		КонецЕсли;
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("НачПериод", 	НачПериод);
		ПараметрыОтчета.Вставить("КонПериод", 	КонПериод);
		ПараметрыОтчета.Вставить("Организация", Организация);
		
		// Получение данных из СКД
		ДанныеОтчета = Новый ДеревоЗначений;
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		// Создадим и инициализируем процессор компоновки.
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
		
		// Обозначим начало вывода
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
		// Вывод отчета
		ВывестиМакет(ДокументРезультат, ДанныеОтчета, ПараметрыОтчета);
		
	Исключение
		Инфо = ИнформацияОбОшибке();
		ВызватьИсключение НСтр("ru = 'В настройку отчета внесены критичные изменения. Отчет не будет сформирован.'") + " " + Инфо.Описание;
	КонецПопытки;
	
КонецПроцедуры
