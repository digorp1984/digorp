#Область РассылкаПисем

// Процедура предназначена для рассылки информационных сообщений пользователям
//
//		Используется механизм БСП. Рассылка писем через внутренний почтовый сервер.
//
Процедура ОтправкаПисем(МассивРассылки)
	
	Для Каждого СтруктураПараметров Из МассивРассылки Цикл
	
		ПараметрыПисьма = СформироватьПараметрыПисьма(СтруктураПараметров);
		Если ПараметрыПисьма = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		РаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(СтруктураПараметров.УчетнаяЗапись, ПараметрыПисьма);
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет возможность отправления письма и если это возможно - формирует параметры отправки.
//
Функция СформироватьПараметрыПисьма(СтруктураПараметров)
	
	ПараметрыПисьма = Новый Структура;
	
	СписокПолучателей = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(СтруктураПараметров.Кому);
	Кому = Новый Массив;
	Для Каждого Получатель Из СписокПолучателей Цикл
		Если Не ПустаяСтрока(Получатель.ОписаниеОшибки) Тогда
			//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Получатель.ОписаниеОшибки, , "ПочтовыйАдресПолучателя");
			Возврат Неопределено;
		КонецЕсли;
		Кому.Добавить(Новый Структура("Адрес, Представление", Получатель.Адрес, Получатель.Псевдоним));
	КонецЦикла;
		
	Если ЗначениеЗаполнено(Кому) Тогда
		ПараметрыПисьма.Вставить("Кому", Кому);
	Иначе
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо заполнить получателя письма'"), , "ПочтовыйАдресПолучателя");
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураПараметров.ТемаПисьма) Тогда
		ПараметрыПисьма.Вставить("Тема", СтруктураПараметров.ТемаПисьма);
	КонецЕсли;
	
	ПараметрыПисьма.Вставить("ПолучателиСообщения", Кому);
	
	ПараметрыПисьма.Вставить("Тело", ТекстВHTML(СтруктураПараметров.ТелоПисьма));
	ПараметрыПисьма.Вставить("ТипТекста", "HTML");
	ПараметрыПисьма.Вставить("Вложения", СтруктураПараметров.Вложения);
	
	Возврат ПараметрыПисьма;
	
КонецФункции
