&НаКлиенте
Процедура ЗагрузитьДоходы(Команда)
	
	//проверка
	Если ПроверитьЗаполнение() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	//параметры
	ОчиститьСообщения();
	Объект.Доходы.Очистить();
	Объект.НовыеФизическиеЛица.Очистить();
	
	//загрузка из ексел
	ТекстСообщения = НСтр("ru = '1 из 2. Чтение данных из файла ...'");
	Состояние(ТекстСообщения);
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.Visible = False;
	    Excel.DisplayAlerts = False;
	Исключение
		ТекстСообщения = НСтр("ru = 'ОШИБКА: Не удалось подключить COMОбъект.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	Попытка
		Workbook = Excel.Workbooks.Open(СокрЛП(Объект.ФайлЗагрузки),,Истина);
	Исключение
		Excel.Quit();
		Excel = Неопределено;
		ТекстСообщения = НСтр("ru = 'ОШИБКА: Ошибка при открытии файла.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
    Лист = Workbook.Sheets(Объект.НомерСтраницы);
    КолвоКолонок = Лист.Cells(1,1).SpecialCells(11).Column;
    КолвоСтрок   = Лист.Cells(1,1).SpecialCells(11).Row;
	НачСтрока = 4;
	КонСтрока = 0;
	
    НачСтрока = ?(НачСтрока = 0, 1, НачСтрока);
    КонСтрока = ?(КонСтрока = 0, КолвоСтрок, КонСтрока);
	КонСтрока = Мин(КонСтрока, КолвоСтрок);
	
    Массив = Лист.Range(Лист.Cells(НачСтрока, 1), Лист.Cells(КонСтрока, КолвоКолонок)).Value; // тип COMSafeArray
    КолвоСтрок = Массив.GetUpperBound(1);

	Для НомерСтроки = 1 По КолвоСтрок Цикл
		
		ФИО = СокрЛП(Массив.GetValue(4, НомерСтроки));
		Если ФИО = "" Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаНалога = 0;
		Попытка
			СуммаНалога = Число(Массив.GetValue(44, НомерСтроки));
		Исключение
		КонецПопытки;
		
		КодДохода = 3020;
		Попытка
			КодДохода = Число(Массив.GetValue(36, НомерСтроки));
		Исключение
		КонецПопытки;
		
		КодВычета = 0;
		Попытка
			КодВычета = Число(Массив.GetValue(38, НомерСтроки));
		Исключение
		КонецПопытки;
		
		Попытка
			
			НоваяСтрока = Объект.Доходы.Добавить();
			НоваяСтрока.ФИО = ФИО;
			НоваяСтрока.ДатаРождения = Массив.GetValue(5, НомерСтроки);
			НоваяСтрока.СуммаДохода = Массив.GetValue(40, НомерСтроки);
			НоваяСтрока.ДатаВыплаты = Массив.GetValue(41, НомерСтроки);
			НоваяСтрока.ДатаУдержания = Массив.GetValue(43, НомерСтроки);
			НоваяСтрока.ДатаПеречисления = Массив.GetValue(45, НомерСтроки);
			НоваяСтрока.СтатусНалогоплатильщика = СокрЛП(Массив.GetValue(42, НомерСтроки));
			НоваяСтрока.СуммаНалога = СуммаНалога;
			НоваяСтрока.НомерПП = Массив.GetValue(46, НомерСтроки);
			НоваяСтрока.СуммаНалогаПП = Массив.GetValue(47, НомерСтроки);
			НоваяСтрока.КодДохода = КодДохода;
			НоваяСтрока.КодВычета = КодВычета;
			
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка чтения строки: 1%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "1%", СокрЛП(НомерСтроки));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	//завершение
	Лист = Неопределено;
	Workbook = Неопределено;
	Excel.Quit();
	Excel = Неопределено;
	
	//обработка на сервере
	ТекстСообщения = НСтр("ru = '2 из 2. Определение ссылок ...'");
	Состояние(ТекстСообщения);
	
	Если Объект.Доходы.Количество() > 0 Тогда
		Объект.Доходы.Сортировать("ФИО,ДатаВыплаты");
		ЗагрузитьДоходыНаСервере();
	КонецЕсли;
	
	//выделение новых физ-лиц
	ЕстьНовыеФЛ = Ложь;
	Для Каждого СтрокаДохода Из Объект.Доходы Цикл
		Если ЗначениеЗаполнено(СтрокаДохода.ФизическоеЛицо) = Ложь Тогда
			ЕстьНовыеФЛ = Истина;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьНовыеФЛ = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = '3. Чтение новых физ-лиц ...'");
	Состояние(ТекстСообщения);
	
	ЧтениеНовыхФизическихЛиц();
	Объект.НовыеФизическиеЛица.Сортировать("ФИО,ДатаРождения");
	ОбработкаАдреса();
	
КонецПроцедуры
