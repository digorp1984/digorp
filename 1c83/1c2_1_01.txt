#Область Этап1_Заполнение

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ОбработкаОбъект	= РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьСотрудниками();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	//проверка
	ОчиститьСообщения();
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	//сообщение
	ТекстСообщения = НСтр("ru = 'Заполнение сотрудниками, которые получали з/п в месяце расчета ...'");
	Состояние(ТекстСообщения);
	
	//первончальное заполнение
	ОчиститьНаКлиенте();
	ЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ПервоначальноеЗаполнениеДанных

Процедура ЗаполнитьСотрудниками(Сотрудники = Неопределено) Экспорт
	
	//выбор основных данных
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодРегистрации", 	ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", 		Организация);
	Запрос.УстановитьПараметр("Сотрудники", 		Сотрудники);
	Запрос.УстановитьПараметр("Отбор1", 			?(Сотрудники = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("Отбор2", 			?(Сотрудники = Неопределено, Ложь, Истина));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленияУдержанияПоСотрудникам.Сотрудник,
	|	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТВыбор
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|ГДЕ
	|	НачисленияУдержанияПоСотрудникам.Активность = ИСТИНА
	|	И НачисленияУдержанияПоСотрудникам.Организация = &Организация
	|	И НачисленияУдержанияПоСотрудникам.Период = &ПериодРегистрации
	|	И &Отбор1 = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияУдержанияПоСотрудникам.ФизическоеЛицо,
	|	НачисленияУдержанияПоСотрудникам.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка,
	|	Сотрудники.ФизическоеЛицо
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&Сотрудники)
	|	И &Отбор2 = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОФК_ИсторияРассылкиРасчетныхЛистков.Сотрудник,
	|	ИСТИНА КАК Отправлено
	|ПОМЕСТИТЬ ВТИстория
	|ИЗ
	|	РегистрСведений.ОФК_ИсторияРассылкиРасчетныхЛистков КАК ОФК_ИсторияРассылкиРасчетныхЛистков
	|ГДЕ
	|	ОФК_ИсторияРассылкиРасчетныхЛистков.МесяцНачисления = &ПериодРегистрации
	|	И ОФК_ИсторияРассылкиРасчетныхЛистков.Организация = &Организация
	|	И ОФК_ИсторияРассылкиРасчетныхЛистков.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТВыбор.Сотрудник
	|			ИЗ
	|				ВТВыбор)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОФК_СотрудникиВнеРассылки.Сотрудник,
	|	ИСТИНА КАК Исключить
	|ПОМЕСТИТЬ ВТИсключения
	|ИЗ
	|	РегистрСведений.ОФК_СотрудникиВнеРассылки КАК ОФК_СотрудникиВнеРассылки
	|ГДЕ
	|	ОФК_СотрудникиВнеРассылки.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТВыбор.Сотрудник
	|			ИЗ
	|				ВТВыбор)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВыбор.Сотрудник,
	|	ВТВыбор.ФизическоеЛицо,
	|	ЕСТЬNULL(ВТИстория.Отправлено, ЛОЖЬ) КАК Отправлено,
	|	0 КАК СуммаКВыплате,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.ОтправлятьТолькоРуководителю,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.РуководительРучнойВыбор,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.Копия1,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.Копия2,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.Копия3,
	|	ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.ПрикрепитьФаилКПисьму,
	|	ЕСТЬNULL(ВТИсключения.Исключить, ЛОЖЬ) КАК Исключить
	|ПОМЕСТИТЬ ВТОбщая
	|ИЗ
	|	ВТВыбор КАК ВТВыбор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИстория КАК ВТИстория
	|		ПО ВТВыбор.Сотрудник = ВТИстория.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам КАК ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам
	|		ПО ВТВыбор.Сотрудник = ОФК_ДетальнаяНастройкаРассылкиРасчетныхЛистковПоСотрудникам.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсключения КАК ВТИсключения
	|		ПО ВТВыбор.Сотрудник = ВТИсключения.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбщая.Сотрудник,
	|	ВТОбщая.ФизическоеЛицо,
	|	ВТОбщая.Отправлено,
	|	ВТОбщая.СуммаКВыплате,
	|	ВТОбщая.ОтправлятьТолькоРуководителю,
	|	ВТОбщая.РуководительРучнойВыбор,
	|	ВТОбщая.Копия1,
	|	ВТОбщая.Копия2,
	|	ВТОбщая.Копия3,
	|	ВТОбщая.ПрикрепитьФаилКПисьму,
	|	ВТОбщая.Сотрудник.Код КАК ТабельныйНомер,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Руководитель
	|ИЗ
	|	ВТОбщая КАК ВТОбщая
	|ГДЕ
	|	ВТОбщая.Исключить = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТОбщая.Сотрудник.Наименование";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	//определение руководителя
	МассиСотрудников = Новый Массив;
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		Если СтрокаДанных.ОтправлятьТолькоРуководителю = Истина И ЗначениеЗаполнено(СтрокаДанных.РуководительРучнойВыбор) = Ложь Тогда
			МассиСотрудников.Добавить(СтрокаДанных.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	МассиСотрудников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассиСотрудников);
	
	СоответсвиеРуководителей = РегистрыСведений.ОФК_Руководители.ПолучитьРукуводителей(МассиСотрудников, КонецМесяца(ПериодРегистрации));
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		СтрокаДанных.Руководитель = СоответсвиеРуководителей.Получить(СтрокаДанных.Сотрудник);
	КонецЦикла;
	
	//заполнение ТЧ
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		НоваяСтрока = РасчетныеЛисты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		
		ТабельныйНомер = СокрЛП(СтрокаДанных.ТабельныйНомер);
		ТабельныйНомер = СтрЗаменить(ТабельныйНомер, "0000-", "");
		НоваяСтрока.ТабельныйНомер = ТабельныйНомер;
		
		Если ЗначениеЗаполнено(СтрокаДанных.РуководительРучнойВыбор) Тогда
			НоваяСтрока.Руководитель = СтрокаДанных.РуководительРучнойВыбор;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
