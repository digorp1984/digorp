Процедура ПроверитьДанныеФизическогоЛицаИФНС(Ссылка, ПроверяемыеДанные, ДатаДокумента, ПутьКДаннымФизическогоЛица, НомерСтроки = Неопределено, Отказ, ОписаниеОшибки)
	
	Ошибки = Новый Соответствие;
	
	ПравилаПроверки = ПравилаПроверкиДанныхФизическогоЛица(ПроверяемыеДанные.Гражданство, ДатаДокумента);
	
	ДанныеФизическогоЛицаДляПроверки = ДанныеФизическогоЛицаДляПроверки(ПроверяемыеДанные);
	
	ФизическиеЛицаЗарплатаКадры.ПроверитьДанныеФизическогоЛица(
		ДанныеФизическогоЛицаДляПроверки,
		ПравилаПроверки,
		Ошибки);
	
	Если НомерСтроки <> Неопределено Тогда
		ПутьКДанным = ПутьКДаннымФизическогоЛица + "[" + Формат(НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
	ИначеЕсли Не ПустаяСтрока(ПутьКДаннымФизическогоЛица) Тогда
		ПутьКДанным = ПутьКДаннымФизическогоЛица + ".";
	Иначе
		ПутьКДанным = ПутьКДаннымФизическогоЛица;
	КонецЕсли;
	
	Для Каждого ОшибкиПоФизЛицу Из Ошибки Цикл
		Для Каждого ОшибкаДанныхФизическогоЛица Из ОшибкиПоФизЛицу.Значение Цикл
			Если ОшибкаДанныхФизическогоЛица.ПолеФормы <> "КемВыдан"
				И ОшибкаДанныхФизическогоЛица.ПолеФормы <> "ДатаВыдачи" Тогда
				
				ОписаниеОшибки = ОписаниеОшибки + ?(ОписаниеОшибки = "", "", Символы.ПС);
				ОписаниеОшибки = ОписаниеОшибки + ОшибкаДанныхФизическогоЛица.ТекстОшибки;
				Отказ = Истина;
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ПроверяемыеДанные.СтатусНалогоплательщика) Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Сотрудник %1: Не заполнено поле ""Статус налогоплательщика""'"),
						ПроверяемыеДанные.СотрудникНаименование);
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + ТекстОшибки;
		Отказ = Истина;
		
	КонецЕсли;
	
	Если ПроверяемыеДанные.Гражданство <> Справочники.СтраныМира.Россия
		И Не ЗначениеЗаполнено(ПроверяемыеДанные.Адрес)
		И Не ЗначениеЗаполнено(ПроверяемыеДанные.АдресЗаРубежом) Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Сотрудник %1: Для физического лица нерезидента или иностранца должен быть заполнен ""Адрес зарубежом""'"),
						ПроверяемыеДанные.СотрудникНаименование);
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + ТекстОшибки;
		Отказ = Истина;
		
	КонецЕсли;
	
	СотрудникТекст = "Сотрудник " + СокрЛП(ПроверяемыеДанные.СотрудникНаименование) + ": ";
	ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, СотрудникТекст, "");
	
КонецПроцедуры
