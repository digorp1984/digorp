//Создание документв Премий, если такие будут в расчитываемом периоде

&НаСервере
Процедура СоздатьДокументРазовоеНачислениеСервер(СозданДокумент)
	
	//параметры
	ПериодРегистрациДок = Объект.ПериодРегистрации;
	СтатьяФинансирования = Справочники.СтатьиФинансированияЗарплата.НайтиПоНаименованию("Основная статья финансирования");
	СпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.НайтиПоНаименованию("Зарплата Организация");
	
	//выбор данных
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДатаОстатков", 	КонецМесяца(ПериодРегистрациДок));
	Запрос.УстановитьПараметр("Выплата", 		Объект.Выплата.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Выплата.Сотрудник,
	|	Выплата.ФизическоеЛицо,
	|	Выплата.СуммаВыплатыИтог КАК Результат,
	|	Выплата.ДатаВыплаты,
	|	Выплата.НомерСтроки
	|ПОМЕСТИТЬ ВТВыплата
	|ИЗ
	|	&Выплата КАК Выплата
	|ГДЕ
	|	Выплата.ДокументНачисления = ЗНАЧЕНИЕ(Документ.РазовоеНачисление.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник,
	|	КадроваяИсторияСотрудниковСрезПоследних.Подразделение
	|ПОМЕСТИТЬ ВТКадровыеДанные
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
	|			&ДатаОстатков,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					ВТВыплата.Сотрудник
	|				ИЗ
	|					ВТВыплата)) КАК КадроваяИсторияСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикРаботыСотрудниковСрезПоследних.Сотрудник,
	|	ГрафикРаботыСотрудниковСрезПоследних.ГрафикРаботы
	|ПОМЕСТИТЬ ВТГрафикиРаботы
	|ИЗ
	|	РегистрСведений.ГрафикРаботыСотрудников.СрезПоследних(
	|			&ДатаОстатков,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					ВТВыплата.Сотрудник
	|				ИЗ
	|					ВТВыплата)) КАК ГрафикРаботыСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВыплата.Сотрудник,
	|	ВТКадровыеДанные.Подразделение,
	|	ВТВыплата.Результат,
	|	ВТВыплата.ФизическоеЛицо,
	|	ВТГрафикиРаботы.ГрафикРаботы,
	|	ВТВыплата.ДатаВыплаты,
	|	ВТВыплата.НомерСтроки
	|ИЗ
	|	ВТВыплата КАК ВТВыплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанные КАК ВТКадровыеДанные
	|		ПО ВТВыплата.Сотрудник = ВТКадровыеДанные.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГрафикиРаботы КАК ВТГрафикиРаботы
	|		ПО ВТВыплата.Сотрудник = ВТГрафикиРаботы.Сотрудник";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		ТекстСообщения = "Нет данных для создания документа - разовое начисление";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	//группы
	МассивДатВыплаты = Новый Массив;
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		Если МассивДатВыплаты.Найти(СтрокаДанных.ДатаВыплаты) = Неопределено Тогда
			МассивДатВыплаты.Добавить(СтрокаДанных.ДатаВыплаты);
		КонецЕсли;
	КонецЦикла;
	
	//создание документа разовое начисление на каждую дату
	Для Каждого ЭлементДатыВыплаты Из МассивДатВыплаты Цикл
		
		//выделение данных по дате выплаты
		СтруктураПоиска = Новый Структура("ДатаВыплаты", ЭлементДатыВыплаты);
		ТаблицаПоДатеВыплаты = ТаблицаДанных.НайтиСтроки(СтруктураПоиска);
		ДокументСсылка = Неопределено;
		
		//шапка документа
		ДокументОбъект = Документы.РазовоеНачисление.СоздатьДокумент();
		ДокументОбъект.Дата = 						НачалоДня(ЭлементДатыВыплаты);
		ДокументОбъект.МесяцНачисления = 			ПериодРегистрациДок;
		ДокументОбъект.Организация = 				Объект.Организация;
		ДокументОбъект.Начисление = 				Объект.Начисление;
		ДокументОбъект.ДатаНачала = 				НачалоМесяца(ПериодРегистрациДок);
		ДокументОбъект.ДатаОкончания = 				КонецМесяца(ПериодРегистрациДок);
		ДокументОбъект.ПорядокВыплаты = 			Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
		ДокументОбъект.ПланируемаяДатаВыплаты = 	ДобавитьМесяц(Дата(Год(ПериодРегистрациДок), Месяц(ПериодРегистрациДок), 5), 1);
		ДокументОбъект.Начислено = 					ТаблицаДанных.Итог("Результат");
		ДокументОбъект.Ответственный = 				Объект.Ответственный;
		ДокументОбъект.Комментарий = 				"Создан автоматически на основании документа: " + СокрЛП(Объект.Ссылка);
		
		//строки ТЧ
		ИдНачисления = 0;
		Для Каждого СтрокаДанных Из ТаблицаПоДатеВыплаты Цикл
			
			ИдНачисления = ИдНачисления + 1;
			
			//	Начисления
			НоваяСтрока = ДокументОбъект.Начисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
			НоваяСтрока.ИдентификаторСтрокиВидаРасчета = ИдНачисления;
			НоваяСтрока.ГрафикРаботыНорма = СтрокаДанных.ГрафикРаботы;
			НоваяСтрока.ВидУчетаВремени = Справочники.ВидыИспользованияРабочегоВремени.РабочееВремя;
			НоваяСтрока.ДатаНачала = ДокументОбъект.ДатаНачала;
			НоваяСтрока.ДатаОкончания = ДокументОбъект.ДатаОкончания;
			НоваяСтрока.ПериодДействия = ДокументОбъект.МесяцНачисления;
			
			//	РаспределениеРезультатовНачислений
			НоваяСтрока2 = ДокументОбъект.РаспределениеРезультатовНачислений.Добавить();
			НоваяСтрока2.ИдентификаторСтроки = ИдНачисления;
			НоваяСтрока2.СтатьяФинансирования = СтатьяФинансирования;
			НоваяСтрока2.СпособОтраженияЗарплатыВБухучете = СпособОтраженияЗарплатыВБухучете;
			НоваяСтрока2.Результат = СтрокаДанных.Результат;
			
		КонецЦикла;
		
		//	ФизическиеЛица
		МассивФЛ = Новый Массив;
		Для Каждого СтрокаДанных Из ТаблицаПоДатеВыплаты Цикл
			Если МассивФЛ.Найти(СтрокаДанных.ФизическоеЛицо) = Неопределено Тогда
				МассивФЛ.Добавить(СтрокаДанных.ФизическоеЛицо);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ФЛ Из МассивФЛ Цикл
			НоваяСтрока3 = ДокументОбъект.ФизическиеЛица.Добавить();
			НоваяСтрока3.ФизическоеЛицо = ФЛ;
		КонецЦикла;
		
		//запись документа
		Попытка
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			ДокументСсылка = ДокументОбъект.Ссылка;
			СозданДокумент = Истина;
			
		Исключение
			
			ДокументСсылка = Неопределено;
			ТекстСообщения = "Не удалось создать документ - Разове начисление";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецПопытки;
		
		//обновление ссылок в табличной части документа
		Если НЕ ДокументСсылка = Неопределено Тогда
			Для Каждого СтрокаДанных Из ТаблицаПоДатеВыплаты Цикл
				Для Каждого СтрокаВыплаты Из Объект.Выплата Цикл
					Если СтрокаДанных.НомерСтроки = СтрокаВыплаты.НомерСтроки Тогда
						СтрокаВыплаты.ДокументНачисления = ДокументСсылка;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
