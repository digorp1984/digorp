ВЫБРАТЬ
	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
ПОМЕСТИТЬ ВТФизическиеЛицаВыбор
ИЗ
	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
ГДЕ
	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Активность = ИСТИНА
	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма <> 0
	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= &НачПериод
	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонПериод
	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.ФизическоеЛицо
ИЗ
	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛ
ГДЕ
	РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Активность = ИСТИНА
	И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Организация = &Организация
	И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Сумма <> 0
	И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= &НачПериод
	И РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонПериод
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТФизическиеЛицаВыбор.ФизическоеЛицо КАК ФизическоеЛицо
ПОМЕСТИТЬ ВТФизическиеЛица
ИЗ
	ВТФизическиеЛицаВыбор КАК ВТФизическиеЛицаВыбор

СГРУППИРОВАТЬ ПО
	ВТФизическиеЛицаВыбор.ФизическоеЛицо
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
	ФизическиеЛица.ИНН КАК ИНН,
	ФизическиеЛица.Фамилия КАК Фамилия,
	ФизическиеЛица.Имя КАК Имя,
	ФизическиеЛица.Отчество КАК Отчество,
	ФизическиеЛица.Ссылка КАК Сотрудник,
	ФизическиеЛица.Наименование КАК СотрудникНаименование,
	ФизическиеЛица.СтраховойНомерПФР КАК СтраховойНомерПФР
ПОМЕСТИТЬ ВТДанныеИзСправочника
ИЗ
	Справочник.ФизическиеЛица КАК ФизическиеЛица
ГДЕ
	ФизическиеЛица.Ссылка В
			(ВЫБРАТЬ
				ВТФизическиеЛица.ФизическоеЛицо
			ИЗ
				ВТФизическиеЛица)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизическоеЛицо,
	ФизическиеЛицаКонтактнаяИнформация.Представление КАК АдресПредставление,
	ФизическиеЛицаКонтактнаяИнформация.ЗначенияПолей КАК Адрес,
	"" КАК АдресЗарубежом
ПОМЕСТИТЬ ВТКонтактнаяИнформация
ИЗ
	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
ГДЕ
	ФизическиеЛицаКонтактнаяИнформация.Ссылка В
			(ВЫБРАТЬ
				ВТФизическиеЛица.ФизическоеЛицо
			ИЗ
				ВТФизическиеЛица)
	И ФизическиеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	И ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДокументыФизическихЛицСрезПоследних.Физлицо КАК ФизическоеЛицо,
	ДокументыФизическихЛицСрезПоследних.ВидДокумента КАК ВидДокумента,
	ДокументыФизическихЛицСрезПоследних.Серия КАК СерияДокумента,
	ДокументыФизическихЛицСрезПоследних.Номер КАК НомерДокумента
ПОМЕСТИТЬ ВТПаспорт
ИЗ
	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
			&КонПериод,
			Физлицо В
				(ВЫБРАТЬ
					ВТФизическиеЛица.ФизическоеЛицо
				ИЗ
					ВТФизическиеЛица)) КАК ДокументыФизическихЛицСрезПоследних
ГДЕ
	ДокументыФизическихЛицСрезПоследних.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ГражданствоФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	ГражданствоФизическихЛицСрезПоследних.Страна КАК Гражданство
ПОМЕСТИТЬ ВТГражданство
ИЗ
	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(
			&КонПериод,
			ФизическоеЛицо В
				(ВЫБРАТЬ
					ВТФизическиеЛица.ФизическоеЛицо
				ИЗ
					ВТФизическиеЛица)) КАК ГражданствоФизическихЛицСрезПоследних
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних.Статус КАК СтатусНалогоплательщика
ПОМЕСТИТЬ ВТСтатусыНалогплатильщиков
ИЗ
	РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
			&КонПериод,
			ФизическоеЛицо В
				(ВЫБРАТЬ
					ВТФизическиеЛица.ФизическоеЛицо
				ИЗ
					ВТФизическиеЛица)) КАК СтатусФизическихЛицКакНалогоплательщиковНДФЛСрезПоследних
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	КадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
	КадроваяИсторияСотрудниковСрезПоследних.Должность КАК Должность,
	КадроваяИсторияСотрудниковСрезПоследних.КоличествоСтавок КАК КоличествоСтавок,
	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК СотрудникСсылка
ПОМЕСТИТЬ ВТКадровыеДанные
ИЗ
	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(
			&КонПериод,
			ФизическоеЛицо В
				(ВЫБРАТЬ
					ВТФизическиеЛица.ФизическоеЛицо
				ИЗ
					ВТФизическиеЛица)) КАК КадроваяИсторияСотрудниковСрезПоследних
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВТФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	ЕСТЬNULL(ВТДанныеИзСправочника.ДатаРождения, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаРождения,
	ЕСТЬNULL(ВТДанныеИзСправочника.ИНН, "") КАК ИНН,
	ЕСТЬNULL(ВТДанныеИзСправочника.Фамилия, "") КАК Фамилия,
	ЕСТЬNULL(ВТДанныеИзСправочника.Имя, "") КАК Имя,
	ЕСТЬNULL(ВТДанныеИзСправочника.Отчество, "") КАК Отчество,
	ВТДанныеИзСправочника.Сотрудник КАК Сотрудник,
	ЕСТЬNULL(ВТДанныеИзСправочника.СотрудникНаименование, "") КАК СотрудникНаименование,
	ЕСТЬNULL(ВТДанныеИзСправочника.СтраховойНомерПФР, "") КАК СтраховойНомерПФР,
	ЕСТЬNULL(ВТКонтактнаяИнформация.АдресПредставление, "") КАК АдресПредставление,
	ЕСТЬNULL(ВТКонтактнаяИнформация.Адрес, "") КАК Адрес,
	ЕСТЬNULL(ВТКонтактнаяИнформация.АдресЗарубежом, "") КАК АдресЗарубежом,
	ЕСТЬNULL(ВТПаспорт.ВидДокумента, ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ)) КАК ВидДокумента,
	ЕСТЬNULL(ВТПаспорт.СерияДокумента, "") КАК СерияДокумента,
	ЕСТЬNULL(ВТПаспорт.НомерДокумента, "") КАК НомерДокумента,
	ЕСТЬNULL(ВТГражданство.Гражданство, ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)) КАК Гражданство,
	ЕСТЬNULL(ВТСтатусыНалогплатильщиков.СтатусНалогоплательщика, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК СтатусНалогоплательщика,
	ВТКадровыеДанные.Подразделение КАК Подразделение,
	ВТКадровыеДанные.Должность КАК Должность,
	ЕСТЬNULL(ВТКадровыеДанные.КоличествоСтавок, 1) КАК КоличествоСтавок,
	ВТКадровыеДанные.СотрудникСсылка КАК СотрудникСсылка
ИЗ
	ВТФизическиеЛица КАК ВТФизическиеЛица
		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеИзСправочника КАК ВТДанныеИзСправочника
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТДанныеИзСправочника.ФизическоеЛицо
		ЛЕВОЕ СОЕДИНЕНИЕ ВТКонтактнаяИнформация КАК ВТКонтактнаяИнформация
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТКонтактнаяИнформация.ФизическоеЛицо
		ЛЕВОЕ СОЕДИНЕНИЕ ВТПаспорт КАК ВТПаспорт
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТПаспорт.ФизическоеЛицо
		ЛЕВОЕ СОЕДИНЕНИЕ ВТГражданство КАК ВТГражданство
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТГражданство.ФизическоеЛицо
		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусыНалогплатильщиков КАК ВТСтатусыНалогплатильщиков
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТСтатусыНалогплатильщиков.ФизическоеЛицо
		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанные КАК ВТКадровыеДанные
		ПО ВТФизическиеЛица.ФизическоеЛицо = ВТКадровыеДанные.ФизическоеЛицо

УПОРЯДОЧИТЬ ПО
	ФизическоеЛицо
