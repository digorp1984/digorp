#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	ИнициализироватьОтчет();

	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();				   

	ДокументРезультат.Очистить();
	
	КлючВарианта = ЗарплатаКадрыОтчеты.КлючВарианта(КомпоновщикНастроек);
		
	//--> ОФК Дебов Игорь 15.06.2017
	//	изменены настройки СКД, выбор данных остался типовой; изменен типовой макет печатной формы.
	//<-- ОФК Дебов Игорь 15.06.2017
	
	// Параметры документа
	ДокументРезультат.ТолькоПросмотр = Истина;
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписокДляСверкиСВоенкоматомОФК";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	ДатаОтчета = '00010101';
	
	УстановитьДатуОтчета(НастройкиОтчета, ДатаОтчета);
	
	//--> ОФК Дебов Игорь 15.06.2017
	ТолькоМосква = Истина;
	ЗначениеПараметраТолькоМосква = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ТолькоМосква"));
	Если ЗначениеПараметраТолькоМосква <> Неопределено Тогда
		ТолькоМосква = ЗначениеПараметраТолькоМосква.Значение;
	КонецЕсли;
	//<-- ОФК Дебов Игорь 15.06.2017
	
	ДанныеОтчета = Новый ДеревоЗначений;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Создадим и инициализируем процессор компоновки.
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	
	// Обозначим начало вывода
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	//--> ОФК Дебов Игорь 15.06.2017
	// Отбор сотрудников с адресом регистрации по Москве
	Если ТолькоМосква = Истина Тогда
		ОтобратьМоскву(ДанныеОтчета);
	КонецЕсли;
	//<-- ОФК Дебов Игорь 15.06.2017
	
	//--> ОФК Дебов Игорь 16.06.2017
	// Изменим и дополним данные по колонкам: годность (7), образование (10), место жительства (11), семейное положение (11).
	ДополнитьДанные(ДанныеОтчета);
	//<-- ОФК Дебов Игорь 16.06.2017
	
	ВывестиМакетСписокДляСверкиСВоенкоматом(ДокументРезультат, ДанныеОтчета, ДатаОтчета);
	
КонецПроцедуры

#КонецОбласти
