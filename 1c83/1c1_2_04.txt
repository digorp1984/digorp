общий модуль - АФМ_ОтражениеЗарплатыВБухучете



#Область СоздатьВТБухучетНачислений

//--> АФМ-Серверс Дебов Игорь Заявка ДМ0000006316 27.06.2016
//	изменяем типовой механизм распределения по статьям финансирования при расчете з/п
Процедура СоздатьВТБухучетОсновногоЗаработка_Изменить(Запрос) Экспорт
	
	//проверка
	Если НЕ СтрНайти(Запрос.Текст, "ВТСтрокиОтраженияАФМ") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//параметры
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	ИндексЗамещаемогоЗапроса = СхемаЗапроса.ПакетЗапросов.Количество();
	ТекстЗапросаРезультат = "";
	
	//модификация запроса
	Для Индекс = 0 По ИндексЗамещаемогоЗапроса - 1 Цикл
		
		ЗапросПакета = СхемаЗапроса.ПакетЗапросов[Индекс];
		
		Если ТипЗнч(ЗапросПакета) = Тип("ЗапросВыбораСхемыЗапроса") Тогда
			
			ТекстЗапроса = ЗапросПакета.ПолучитьТекстЗапроса();
			Если СокрЛП(ЗапросПакета.ТаблицаДляПомещения)="ВТСтрокиОтражения" Тогда
				ТекстЗапроса =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ВТСтрокиОтраженияАФМ.ИдентификаторСтроки,
				|	БухучетРаспределениеОсновногоЗаработка.СтатьяФинансирования,
				|	БухучетРаспределениеОсновногоЗаработка.СпособОтраженияЗарплатыВБухучете,
				|	БухучетРаспределениеОсновногоЗаработка.ОблагаетсяЕНВД,
				|	БухучетРаспределениеОсновногоЗаработка.ДоляРаспределения
				|ПОМЕСТИТЬ ВТСтрокиОтражения
				|ИЗ
				|	(ВЫБРАТЬ
				|		Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|		Начисления.Сотрудник КАК Сотрудник,
				|		Начисления.Организация КАК Организация,
				|		МАКСИМУМ(РаспределениеОсновногоЗаработка.ПериодРегистрации) КАК ПериодРаспределения
				|	ИЗ
				|		ВТНачисленияДляРаспределения КАК Начисления
				|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
				|			ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК РаспределениеОсновногоЗаработка
				|			ПО Начисления.Сотрудник = РаспределениеОсновногоЗаработка.Сотрудник
				|				И Начисления.Организация = РаспределениеОсновногоЗаработка.Организация
				|				И Начисления.ПериодРегистрации >= РаспределениеОсновногоЗаработка.ПериодРегистрации
				|	ГДЕ
				|		СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL 
				|	
				|	СГРУППИРОВАТЬ ПО
				|		Начисления.ИдентификаторСтроки,
				|		Начисления.Организация,
				|		Начисления.Сотрудник) КАК ВТСтрокиОтраженияАФМ
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК БухучетРаспределениеОсновногоЗаработка
				|		ПО ВТСтрокиОтраженияАФМ.Сотрудник = БухучетРаспределениеОсновногоЗаработка.Сотрудник
				|			И ВТСтрокиОтраженияАФМ.Организация = БухучетРаспределениеОсновногоЗаработка.Организация
				|			И ВТСтрокиОтраженияАФМ.ПериодРаспределения = БухучетРаспределениеОсновногоЗаработка.ПериодРегистрации";
			КонецЕсли;
			ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(ТекстЗапросаРезультат, ТекстЗапроса);	
			
		Иначе
			
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросУничтоженияВременнойТаблицы(ТекстЗапросаРезультат, СокрЛП(ЗапросПакета.ИмяТаблицы));	
			
		КонецЕсли;
		
	КонецЦикла;
	
	//завершение
	Запрос.Текст = ТекстЗапросаРезультат;
	
КонецПроцедуры
//<-- АФМ-Серверс Дебов Игорь Заявка ДМ0000006316 27.06.2016

//--> АФМ-Серверс Дебов Игорь Заявка ДМ0000008267 31.08.2016
Процедура СоздатьВТБухучетРасходыПоСтрахованию_Изменить(Запрос) Экспорт
	
	//доп. параметры
	Запрос.УстановитьПараметр("СтатьяОсновнойДеятельностиАФМ", ПолучитьСтатьюОсновнойДеятельности(Запрос.Параметры.Организация));
	
	//меняем итоговую таблицу ВТБухучетПоСреднемуЗаработку
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТБухучетРасходыПоСтрахованию.Сотрудник,
	|	ВТБухучетРасходыПоСтрахованию.ПериодРегистрации КАК ПериодДействия,
	|	ИСТИНА КАК ПолеОтбор
	|ПОМЕСТИТЬ ВТСтраховыеСлучаиАФМ
	|ИЗ
	|	ВТБухучетРасходыПоСтрахованию КАК ВТБухучетРасходыПоСтрахованию
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|					ИЛИ ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования ЕСТЬ NULL 
	|					ИЛИ ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетРаспределениеОсновногоЗаработка.Сотрудник,
	|	БухучетРаспределениеОсновногоЗаработка.АФМ_ПериодНачало,
	|	БухучетРаспределениеОсновногоЗаработка.СтатьяФинансирования
	|ПОМЕСТИТЬ ВТБухучетАФМ
	|ИЗ
	|	РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК БухучетРаспределениеОсновногоЗаработка
	|ГДЕ
	|	БухучетРаспределениеОсновногоЗаработка.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТСтраховыеСлучаиАФМ.Сотрудник
	|			ИЗ
	|				ВТСтраховыеСлучаиАФМ)
	|	И БухучетРаспределениеОсновногоЗаработка.Активность = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучетАФМ.Сотрудник,
	|	МАКСИМУМ(ВТБухучетАФМ.АФМ_ПериодНачало) КАК АФМ_ПериодНачало,
	|	ИСТИНА КАК ПолеОтбор
	|ПОМЕСТИТЬ ВТБухучетСрезПоследнихАФМ
	|ИЗ
	|	ВТБухучетАФМ КАК ВТБухучетАФМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтраховыеСлучаиАФМ КАК ВТСтраховыеСлучаиАФМ
	|		ПО ВТБухучетАФМ.Сотрудник = ВТСтраховыеСлучаиАФМ.Сотрудник
	|			И ВТБухучетАФМ.АФМ_ПериодНачало <= ВТСтраховыеСлучаиАФМ.ПериодДействия
	|ГДЕ
	|	ЕСТЬNULL(ВТСтраховыеСлучаиАФМ.ПолеОтбор, ЛОЖЬ) = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТБухучетАФМ.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучетАФМ.Сотрудник,
	|	ВТБухучетАФМ.СтатьяФинансирования
	|ПОМЕСТИТЬ ВТБухучетИтогАФМ
	|ИЗ
	|	ВТБухучетАФМ КАК ВТБухучетАФМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетСрезПоследнихАФМ КАК ВТБухучетСрезПоследнихАФМ
	|		ПО ВТБухучетАФМ.Сотрудник = ВТБухучетСрезПоследнихАФМ.Сотрудник
	|			И ВТБухучетАФМ.АФМ_ПериодНачало = ВТБухучетСрезПоследнихАФМ.АФМ_ПериодНачало
	|ГДЕ
	|	ЕСТЬNULL(ВТБухучетСрезПоследнихАФМ.ПолеОтбор, ЛОЖЬ) = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучетРасходыПоСтрахованию.ПериодРегистрации,
	|	ВТБухучетРасходыПоСтрахованию.ИдентификаторСтроки,
	|	ВТБухучетРасходыПоСтрахованию.Организация,
	|	ВТБухучетРасходыПоСтрахованию.Сотрудник,
	|	ВТБухучетРасходыПоСтрахованию.ФизическоеЛицо,
	|	ВТБухучетРасходыПоСтрахованию.Подразделение,
	|	ВТБухучетРасходыПоСтрахованию.ТерриторияВыполненияРаботВОрганизации,
	|	ВТБухучетРасходыПоСтрахованию.Начисление,
	|	ВТБухучетРасходыПоСтрахованию.ВидОперации,
	|	ВТБухучетРасходыПоСтрахованию.ДатаНачала,
	|	ВТБухучетРасходыПоСтрахованию.ДатаОкончания,
	|	ВТБухучетРасходыПоСтрахованию.ДокументОснование,
	|	ВТБухучетРасходыПоСтрахованию.СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования ЕСТЬ NULL 
	|				ИЛИ ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЕСТЬNULL(ВТБухучетИтогАФМ.СтатьяФинансирования, &СтатьяОсновнойДеятельностиАФМ)
	|		ИНАЧЕ ВТБухучетРасходыПоСтрахованию.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВТБухучетРасходыПоСтрахованию.СтатьяРасходов,
	|	ВТБухучетРасходыПоСтрахованию.Сумма,
	|	ВТБухучетРасходыПоСтрахованию.ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТБухучетРасходыПоСтрахованиюАФМ
	|ИЗ
	|	ВТБухучетРасходыПоСтрахованию КАК ВТБухучетРасходыПоСтрахованию
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетИтогАФМ КАК ВТБухучетИтогАФМ
	|		ПО ВТБухучетРасходыПоСтрахованию.Сотрудник = ВТБухучетИтогАФМ.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтраховыеСлучаиАФМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетАФМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетСрезПоследнихАФМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетИтогАФМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетРасходыПоСтрахованию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ПериодРегистрации,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ИдентификаторСтроки,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.Организация,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.Сотрудник,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ФизическоеЛицо,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.Подразделение,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ТерриторияВыполненияРаботВОрганизации,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.Начисление,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ВидОперации,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ДатаНачала,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ДатаОкончания,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ДокументОснование,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.СпособОтраженияЗарплатыВБухучете,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.СтатьяФинансирования,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.СтатьяРасходов,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.Сумма,
	|	ВТБухучетРасходыПоСтрахованиюАФМ.ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТБухучетРасходыПоСтрахованию
	|ИЗ
	|	ВТБухучетРасходыПоСтрахованиюАФМ КАК ВТБухучетРасходыПоСтрахованиюАФМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетРасходыПоСтрахованиюАФМ";
	
	//завершение
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПолучитьСтатьюОсновнойДеятельности(Организация) Экспорт
	
	СтатьяОсновнойДеятельности = Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
	Если НЕ Организация = Справочники.Организации.ПустаяСсылка() Тогда
		ОрганизацияПрефикс = СокрЛП(Организация.Префикс);
		СтатьяОсновнойДеятельности = Справочники.СтатьиФинансированияЗарплата.НайтиПоКоду("О" + ОрганизацияПрефикс);
	КонецЕсли;
	
	Возврат СтатьяОсновнойДеятельности;
	
КонецФункции
//<-- АФМ-Серверс Дебов Игорь Заявка ДМ0000008267 31.08.2016

#КонецОбласти
