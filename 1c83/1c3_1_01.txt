&НаСервере
Процедура СотрудникЗаполнитьПараметры()
	
	//параметры
	ДатаДанных = КонецДня(ТекущаяДата()) + 1;
	
	СписокФизическихЛиц = Новый Массив;
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		СписокФизическихЛиц.Добавить(СтрокаСотрудника.Сотрудник.ФизическоеЛицо);
	КонецЦикла;
	
	//получаем данные по программному интерфейсу
	ИмяВТСотрудникиОрганизации = "ВТСотрудникиОрганизацииПредварительно";
	КадровыеДанные = "ГоловнаяОрганизация,Организация,Фамилия,Имя,Отчество,АдресПоПропискеПредставление,";
	КадровыеДанные = КадровыеДанные + "ДатаДоговораКонтракта,НомерДоговораКонтракта,";
	КадровыеДанные = КадровыеДанные + "ДокументСерия,ДокументНомер,ДокументДатаВыдачи,ДокументКемВыдан,ДокументКодПодразделения,";
	КадровыеДанные = КадровыеДанные + "ТекущаяТарифнаяСтавка,";
	КадровыеДанные = КадровыеДанные + "Подразделение,Должность";
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода = ДатаДанных;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода = ДатаДанных;
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные = КадровыеДанные;
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц = СписокФизическихЛиц;
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций, ИмяВТСотрудникиОрганизации);
	
	//формируем таблицу параметров
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Сотрудник КАК Справочник.Сотрудники) КАК Сотрудник,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ФизическоеЛицо КАК Справочник.ФизическиеЛица) КАК ФизическоеЛицо,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ГоловнаяОрганизация КАК Справочник.Организации) КАК ГоловнаяОрганизация,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Организация КАК Справочник.Организации) КАК Организация,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Фамилия КАК СТРОКА(150)) КАК Фамилия,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Имя КАК СТРОКА(150)) КАК Имя,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Отчество КАК СТРОКА(150)) КАК Отчество,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДокументСерия КАК СТРОКА(20)) КАК ПаспортСерия,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДокументНомер КАК СТРОКА(50)) КАК ПаспортНомер,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДокументДатаВыдачи КАК ДАТА) КАК ПаспортДатаВыдачи,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДокументКемВыдан КАК СТРОКА(255)) КАК ПаспортВыдан,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДокументКодПодразделения КАК СТРОКА(30)) КАК ПаспортКод,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.АдресПоПропискеПредставление КАК СТРОКА(255)) КАК Адрес,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ДатаДоговораКонтракта КАК ДАТА) КАК ДоговорДата,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.НомерДоговораКонтракта КАК СТРОКА(50)) КАК ДоговорНомер,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ТекущаяТарифнаяСтавка КАК ЧИСЛО(15, 2)) КАК Оклад,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Подразделение КАК Справочник.ПодразделенияОрганизаций) КАК Подразделение,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Должность КАК Справочник.Должности) КАК Должность
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	ВТСотрудникиОрганизацииПредварительно КАК ВТСотрудникиОрганизацииПредварительно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемНаРаботу.Сотрудник,
	|	ПриемНаРаботу.ДлительностьИспытательногоСрока КАК ИспытательныйСрок
	|ПОМЕСТИТЬ ВТДополнительныеДанные
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	ПриемНаРаботу.ПометкаУдаления = ЛОЖЬ
	|	И ПриемНаРаботу.Проведен = ИСТИНА
	|	И ПриемНаРаботу.Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТСотрудники.Сотрудник
	|			ИЗ
	|				ВТСотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СклоненияПредставленийОбъектов.Объект КАК Должность,
	|	СклоненияПредставленийОбъектов.РодительныйПадеж
	|ПОМЕСТИТЬ ВТСклоненияДолжностей
	|ИЗ
	|	РегистрСведений.СклоненияПредставленийОбъектов КАК СклоненияПредставленийОбъектов
	|ГДЕ
	|	СклоненияПредставленийОбъектов.Объект В
	|			(ВЫБРАТЬ
	|				ВТСотрудники.Должность
	|			ИЗ
	|				ВТСотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСотрудники.Сотрудник,
	|	ВТСотрудники.ФизическоеЛицо,
	|	ВТСотрудники.ГоловнаяОрганизация,
	|	ВТСотрудники.Организация,
	|	ВТСотрудники.Фамилия КАК Фамилия,
	|	ВТСотрудники.Имя КАК Имя,
	|	ВТСотрудники.Отчество,
	|	ВТСотрудники.ПаспортСерия,
	|	ВТСотрудники.ПаспортНомер,
	|	ВТСотрудники.ПаспортДатаВыдачи,
	|	ВТСотрудники.ПаспортВыдан,
	|	ВТСотрудники.ПаспортКод,
	|	ВТСотрудники.Адрес,
	|	ВТСотрудники.ДоговорДата,
	|	ВТСотрудники.ДоговорНомер,
	|	ВТСотрудники.Оклад,
	|	ВТСотрудники.Подразделение,
	|	ВТСотрудники.Должность,
	|	ЕСТЬNULL(ВТДополнительныеДанные.ИспытательныйСрок, 0) КАК ИспытательныйСрок
	|ИЗ
	|	ВТСотрудники КАК ВТСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДополнительныеДанные КАК ВТДополнительныеДанные
	|		ПО ВТСотрудники.Сотрудник = ВТДополнительныеДанные.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСклоненияДолжностей КАК ВТСклоненияДолжностей
	|		ПО ВТСотрудники.Должность = ВТСклоненияДолжностей.Должность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Фамилия,
	|	Имя";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаПараметров = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	//заполняем значения объекта
	Для Каждого СтрокаПараметров Из ТаблицаПараметров Цикл
		
		//данные по сотруднику
		ТекущаяСтрока = Неопределено;
		
		Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
			Если СтрокаСотрудника.Сотрудник = СтрокаПараметров.Сотрудник Тогда
				ТекущаяСтрока = СтрокаСотрудника;
			КонецЕсли;
		КонецЦикла;
		
		Если ТекущаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		//основные параметры
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаПараметров);
		
		//поле - ФИО
		ТекущаяСтрока.ФИО = СтрокаПараметров.Фамилия + " " + СтрокаПараметров.Имя + " " + СтрокаПараметров.Отчество;
		ТекущаяСтрока.ИОФамилия = ВРег(Лев(СтрокаПараметров.Имя, 1)) + "." + ВРег(Лев(СтрокаПараметров.Отчество, 1)) + ". " + СтрокаПараметров.Фамилия;
		
	КонецЦикла;
	
КонецПроцедуры
