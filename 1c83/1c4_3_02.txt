#Область ОбработчикиКомандФормы_ВыходноеПособие

&НаКлиенте
Процедура Рассчитать3(Команда)
	
	//проверка
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	Если Объект.РасчетСреднегоПриУвольнении.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//расчет
	РассчитатьСреднегоДляВыхПособия();
	
	//расчет ТЧ
	РасчетСуммТЧ3();
	
	//обновление картинки кнопки
	ЗарплатаКадрыРасширенныйКлиентСервер.УстановитьСостояниеКнопкиФормыРассчитать(ЭтаФорма, Ложь, "Рассчитать3");
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСреднегоДляВыхПособия()
	
	//параметры
	ВидРасчета = ПланыВидовРасчета.Начисления.НайтиПоНаименованию("Командировка");
	
	//определение периода
	ПериодРасчетаСреднего = УчетСреднегоЗаработка.ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(Объект.ДатаУвольнения, Объект.РасчетСреднегоПриУвольнении[0].Сотрудник, ВидРасчета);
	Объект.ПериодРасчетаСреднегоЗаработкаНачало	= ПериодРасчетаСреднего.ДатаНачала;
	Объект.ПериодРасчетаСреднегоЗаработкаОкончание = ПериодРасчетаСреднего.ДатаОкончания;
	
	//заполнение исходных данных расчета по сотруднику
	ОбновитьДанныеДляРасчетаСреднегоВыхПособия();
	
	//расчет по каждому сотруднику
	Для Каждого СтрокаДанных Из Объект.РасчетСреднегоПриУвольнении Цикл
		
		//подготовка таблиц
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Сотрудник", СтрокаДанных.Сотрудник);
		
		ТаблицаДанныеОбИндексации = Объект.ДанныеОбИндексации.Выгрузить(СтруктураПоиска);
		ТаблицаСреднийЗаработокОбщий = Объект.СреднийЗаработокОбщий.Выгрузить(СтруктураПоиска);
		ТаблицаОтработанноеВремяДляСреднегоОбщий = Объект.ОтработанноеВремяДляСреднегоОбщий.Выгрузить(СтруктураПоиска);
		
		//расчет суммы среднего
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.Индексации = ТаблицаДанныеОбИндексации;
		ДополнительныеПараметры.ДатаНачалаСобытия = Объект.ДатаУвольнения;
		ДополнительныеПараметры.НачалоПериода = Объект.ПериодРасчетаСреднегоЗаработкаНачало;
		ДополнительныеПараметры.ОкончаниеПериода = Объект.ПериодРасчетаСреднегоЗаработкаОкончание;
		ДополнительныеПараметры.ПоЧасам = Ложь;
	
		СтрокаДанных.СуммаСреднего = УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработокОбщий(ТаблицаСреднийЗаработокОбщий, ТаблицаОтработанноеВремяДляСреднегоОбщий, ДополнительныеПараметры);
		
		//установка итогов по строке
		СтрокаДанных.Результат1 = 0;
		СтрокаДанных.Результат2 = 0;
		СтрокаДанных.Результат = 0;
		СтрокаДанных.НДФЛ = 0;
		СтрокаДанных.СуммаОклада = 0;
		
	КонецЦикла;
	
	//определение оклада (кадровый метод)
	СписокФизическихЛиц = Новый Массив;
	Для Каждого СтрокаДанных Из Объект.РасчетСреднегоПриУвольнении Цикл
		СписокФизическихЛиц.Добавить(СтрокаДанных.Сотрудник.ФизическоеЛицо);
	КонецЦикла;
	
	ИмяВТСотрудникиОрганизации = "ВТСотрудникиОрганизацииПредварительно";
	КадровыеДанные = "Организация,ТарифнаяСтавка";
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц = СписокФизическихЛиц;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода = НачалоДня(Объект.ДатаУвольнения) - 1;
	ПараметрыПолученияСотрудниковОрганизаций.КадровыеДанные = КадровыеДанные;
	
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций, ИмяВТСотрудникиОрганизации);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Сотрудник КАК Справочник.Сотрудники) КАК Сотрудник,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ФизическоеЛицо КАК Справочник.ФизическиеЛица) КАК ФизическоеЛицо,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.Организация КАК Справочник.Организации) КАК Организация,
	|	ВЫРАЗИТЬ(ВТСотрудникиОрганизацииПредварительно.ТарифнаяСтавка КАК ЧИСЛО(15, 2)) КАК Оклад
	|ИЗ
	|	ВТСотрудникиОрганизацииПредварительно КАК ВТСотрудникиОрганизацииПредварительно";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаОкладов = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого СтрокаДанных Из Объект.РасчетСреднегоПриУвольнении Цикл
		
		СтруктураПоиска = Новый Структура("Сотрудник", СтрокаДанных.Сотрудник);
		НайденныйОклад = ТаблицаОкладов.НайтиСтроки(СтруктураПоиска);
		Если НайденныйОклад.Количество() > 0 Тогда
			СтрокаДанных.СуммаОклада = НайденныйОклад[0].Оклад;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляРасчетаСреднегоВыхПособия()
	
	МассивСотрудников = Новый Массив;
	Для Каждого СтрокаДанных Из Объект.РасчетСреднегоПриУвольнении Цикл
		МассивСотрудников.Добавить(СтрокаДанных.Сотрудник);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УчетСреднегоЗаработка.ОбновитьДанныеОбщегоСреднегоЗаработка(
			Новый Структура("ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации", 
			Объект.СреднийЗаработокОбщий, Объект.ОтработанноеВремяДляСреднегоОбщий, Объект.ДанныеОбИндексации), 
			Объект.ДатаНачалаСобытия, 
			Объект.ПериодРасчетаСреднегоЗаработкаНачало, 
			Объект.ПериодРасчетаСреднегоЗаработкаОкончание,
			МассивСотрудников, , 
			Документы.Командировка.ПустаяСсылка());

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры	

&НаСервере
Процедура РасчетСуммТЧ3()
	
	Для Каждого СтрокаДанных Из Объект.РасчетСреднегоПриУвольнении Цикл
		
		//расчет общего итога по кол-ву необлагаемых сумм
		Если Объект.КолвоДнейОблагаемых <> 0 Тогда
			
			СтрокаДанных.Результат1 = СтрокаДанных.СуммаСреднего * Объект.КолвоДнейНеОблагаемых;
			СтрокаДанных.Результат2 = СтрокаДанных.СуммаСреднего * Объект.КолвоДнейОблагаемых;
			СтрокаДанных.Результат = СтрокаДанных.Результат1 + СтрокаДанных.Результат2;
			СтрокаДанных.НДФЛ = Окр(СтрокаДанных.Результат2 * 0.13, 0);
			
		КонецЕсли;
		
		//расчет предела от оклада
		Если Объект.КолвоОкладов <> 0 Тогда
			
			ВсегоВыплаты = Объект.КолвоОкладов * СтрокаДанных.СуммаОклада;
			
			СтрокаДанных.Результат1 = СтрокаДанных.СуммаСреднего * Объект.КолвоДнейНеОблагаемых;
			Если СтрокаДанных.Результат1 > ВсегоВыплаты Тогда
				СтрокаДанных.Результат1 = ВсегоВыплаты;
			КонецЕсли;
			
			СтрокаДанных.Результат2 = ВсегоВыплаты - СтрокаДанных.Результат1;
			Если СтрокаДанных.Результат2 < 0 Тогда
				СтрокаДанных.Результат2 = 0;
			КонецЕсли;
			
			СтрокаДанных.Результат = ВсегоВыплаты;
			СтрокаДанных.НДФЛ = Окр(СтрокаДанных.Результат2 * 0.13, 0);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
