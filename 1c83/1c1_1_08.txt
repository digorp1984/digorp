----------------------------------------------
установка первичного шаблона назначения ПП
----------------------------------------------

&НаСервере
Процедура УстановитьШаблонНазначенияПлатежа()
	
	//проверка
	Объект.ШаблонНазначениеПлатежа = "";
	Если НЕ ЗначениеЗаполнено(Объект.Сотрудник) ИЛИ СокрЛП(Объект.НомерСчетаПолучателя) = "" Тогда
		Возврат;
	КонецЕсли;
	
	//получение ФИО в род. падеже
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ФизЛицо", Объект.Сотрудник.ФизическоеЛицо);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СклоненияПредставленийОбъектов.Объект КАК ФизическоеЛицо,
	|	СклоненияПредставленийОбъектов.РодительныйПадеж КАК РодительныйПадеж
	|ИЗ
	|	РегистрСведений.СклоненияПредставленийОбъектов КАК СклоненияПредставленийОбъектов
	|ГДЕ
	|	СклоненияПредставленийОбъектов.Объект = &ФизЛицо";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаСклонений = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ФИОРП = "___________________";
	Если ТаблицаСклонений.Количество() > 0 Тогда
		ФИОРП = СокрЛП(ТаблицаСклонений[0].РодительныйПадеж);
	КонецЕсли;
	
	//номер счета
	НомерСчета = СокрЛП(Объект.НомерСчетаПолучателя);
	
	//формирование назначения платежа
	ШаблонНазначениеПлатежа = "Перечисление заработной платы на счет № " + НомерСчета + " на имя " + ФИОРП + " за [Период1]г, [СметаЦБ] года Без НДС";
	Объект.ШаблонНазначениеПлатежа = ШаблонНазначениеПлатежа;
	
КонецПроцедуры

-------------------------------------
установка нового место выплаты
-------------------------------------

&НаСервере
Процедура УстановитьНовоеНаСервере(ВыполеноУспешно)
	
	//рег. св. - ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам
	НаборЗаписей1 = РегистрыСведений.ЛицевыеСчетаСотрудниковПоЗарплатнымПроектам.СоздатьНаборЗаписей();
	НаборЗаписей1.Отбор.Период.Установить(НачалоМесяца(ПериодНачала));
	НаборЗаписей1.Отбор.Организация.Установить(Организация);
	НаборЗаписей1.Отбор.ФизическоеЛицо.Установить(Сотрудник.ФизическоеЛицо);
	НаборЗаписей1.Прочитать();
	
	НаборЗаписей1.Очистить();
	
	НовыйЛицевойСчет = НаборЗаписей1.Добавить();
	НовыйЛицевойСчет.Период = 				НачалоМесяца(ПериодНачала);
	НовыйЛицевойСчет.Организация = 			Организация;
	НовыйЛицевойСчет.ФизическоеЛицо = 		Сотрудник.ФизическоеЛицо;
	НовыйЛицевойСчет.НомерЛицевогоСчета = 	СокрЛП(НомерЛицевогоСчета);
	НовыйЛицевойСчет.ЗарплатныйПроект = 	ЗарплатныйПроект;
	
	Попытка
		НаборЗаписей1.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка записи лицевого счета!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполеноУспешно = Ложь;
	КонецПопытки;
	
	//рег. св. - МестаВыплатыЗарплатыСотрудников
	НаборЗаписей2 = РегистрыСведений.МестаВыплатыЗарплатыСотрудников.СоздатьНаборЗаписей();
	НаборЗаписей2.Отбор.Сотрудник.Установить(Сотрудник);
	НаборЗаписей2.Отбор.ФизическоеЛицо.Установить(Сотрудник.ФизическоеЛицо);
	НаборЗаписей2.Прочитать();
	
	НаборЗаписей2.Очистить();
	
	НовыйЛицевойСчет = НаборЗаписей2.Добавить();
	НовыйЛицевойСчет.Сотрудник = 		Сотрудник;
	НовыйЛицевойСчет.ФизическоеЛицо = 	Сотрудник.ФизическоеЛицо;
	НовыйЛицевойСчет.Вид = 				ВидМестаВыплаты;
	НовыйЛицевойСчет.МестоВыплаты = 	ЗарплатныйПроект;
	
	Попытка
		НаборЗаписей2.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка записи лицевого счета!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполеноУспешно = Ложь;
	КонецПопытки;
	
КонецПроцедуры
