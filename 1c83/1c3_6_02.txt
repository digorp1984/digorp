#Область ФормированиеОсновногоОтчета

Процедура ВывестиМакет(ДокументРезультат, ДанныеОтчета, ПараметрыОтчета)
	
	//параметры отчета
	ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АФМ_ПланированиеЗатратНаПерсонал";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Макет = ПолучитьМакет("Макет_ПланированиеЗатратНаПерсонал");
	Макеты = Новый Структура("ОбластьЛист");
	Макеты.ОбластьЛист = Макет.ПолучитьОбласть("ОбластьЛист");
	
	ПараметрыРасчет = Новый Структура;
	Для НомерСтроки = 1 По 8 Цикл
		Для НомерКолонки = 0 По 4 Цикл
			//где НОЛЬ это колонка за 4 квартал, предыдущего года
			КлючПараметра = СокрЛП(НомерСтроки) + "_" + СокрЛП(НомерКолонки);
			ПараметрыРасчет.Вставить("Резервирование" + КлючПараметра, 0);
			ПараметрыРасчет.Вставить("Расход" + КлючПараметра, 0);
			ПараметрыРасчет.Вставить("Резерв" + КлючПараметра, 0);
		КонецЦикла;
	КонецЦикла;
	
	//параметры расчета
	МассивПремияЗаМесяц = АФМ_ИзменениеТиповогоФункционала.СостояниеШтатногоРасписания_ШтатнаяРасстановка_СформироватьМассивНастроек(ПланыВидовХарактеристик.АФМ_ПредопределенныеДанные.ШтатнаяРасстановкаКолонкаПремияЗаМесяц, 1);
	МассивКвартальнаяПремия = АФМ_ИзменениеТиповогоФункционала.СостояниеШтатногоРасписания_ШтатнаяРасстановка_СформироватьМассивНастроек(ПланыВидовХарактеристик.АФМ_ПредопределенныеДанные.ШтатнаяРасстановкаКолонкаКвартальнаяПремия, 1);
	МассивВыплатаПоРН = АФМ_ИзменениеТиповогоФункционала.СостояниеШтатногоРасписания_ШтатнаяРасстановка_СформироватьМассивНастроек(ПланыВидовХарактеристик.АФМ_ПредопределенныеДанные.ШтатнаяРасстановкаКолонкаВыплатаПоРН, 1);
	МассивВыплатаПоРК = ПолучитьПоказателиДляРасчета("РайонныйКоэффициент,РайонныйКоэффициентРФ");
	МассивВыплатаПоСН = ПолучитьПоказателиДляРасчета("СевернаяНадбавка");
	
	МассивПоказателей = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПоказателей, МассивПремияЗаМесяц);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПоказателей, МассивКвартальнаяПремия);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПоказателей, МассивВыплатаПоРН);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПоказателей, МассивВыплатаПоРК);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПоказателей, МассивВыплатаПоСН);
	
	//расчет ставок ПФР
	СтавкаПФР = 0;
	СтавкаФССНС = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПериодНачало", ПараметрыОтчета.НачПериод);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТарифыСтраховыхВзносовСрезПоследних.ПФР,
	|	ТарифыСтраховыхВзносовСрезПоследних.ПФРНакопительная,
	|	ТарифыСтраховыхВзносовСрезПоследних.ФСС,
	|	ТарифыСтраховыхВзносовСрезПоследних.ФФОМС,
	|	ТарифыСтраховыхВзносовСрезПоследних.ТФОМС,
	|	ТарифыСтраховыхВзносовСрезПоследних.ПФР + ТарифыСтраховыхВзносовСрезПоследних.ПФРНакопительная + ТарифыСтраховыхВзносовСрезПоследних.ФСС + ТарифыСтраховыхВзносовСрезПоследних.ФФОМС + ТарифыСтраховыхВзносовСрезПоследних.ТФОМС КАК ОбщаяСтавка
	|ИЗ
	|	РегистрСведений.ТарифыСтраховыхВзносов.СрезПоследних(&ПериодНачало, ВидТарифа = ЗНАЧЕНИЕ(Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим)) КАК ТарифыСтраховыхВзносовСрезПоследних";
	
	ТаблицаПФР = Запрос.Выполнить().Выгрузить();
	Если ТаблицаПФР.Количество() > 0 Тогда
		СтавкаПФР = ТаблицаПФР[0].ОбщаяСтавка / 100;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевСрезПоследних.Организация,
	|	СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевСрезПоследних.Ставка
	|ИЗ
	|	РегистрСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.СрезПоследних(&ПериодНачало, Организация = &Организация) КАК СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевСрезПоследних";
	
	ТаблицаФССНС = Запрос.Выполнить().Выгрузить();
	Если ТаблицаФССНС.Количество() > 0 Тогда
		СтавкаФССНС = ТаблицаФССНС[0].Ставка / 100;
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("СтавкаПФР", СтавкаПФР);
	ПараметрыОтчета.Вставить("СтавкаФССНС", СтавкаФССНС);
	
	//расчет плана
	РасчетПлана(ПараметрыОтчета, ПараметрыРасчет, МассивПоказателей, МассивПремияЗаМесяц, МассивКвартальнаяПремия, МассивВыплатаПоРН, МассивВыплатаПоРК, МассивВыплатаПоСН);
	
	//расчет факта
	РасчетФакта(ПараметрыОтчета, ПараметрыРасчет, МассивПоказателей, МассивПремияЗаМесяц, МассивКвартальнаяПремия, МассивВыплатаПоРН, МассивВыплатаПоРК, МассивВыплатаПоСН);
	
	//чтение ввода остатков
	ЗаполнениеОстатков(ПараметрыОтчета, ПараметрыРасчет);
	
	//расчет резерва
	//	Резерв 1 квартала = Резервирование 1 квартала - Резервирование 4 кв. предыдущего года - Расход 1 квартала
	//	Резерв 2 квартала = Резервирование 2 квартала - Резервирование 1 квартала текущего года - Расход 2 квартала
	Для НомерСтроки = 1 По 8 Цикл
		
		Для НомерКвартала = 1 По 4 Цикл
			КлючПараметра = СокрЛП(НомерСтроки) + "_" + СокрЛП(НомерКвартала);
			СуммаРезерва = ПараметрыРасчет["Резервирование" + КлючПараметра];
			СуммаРезерва = СуммаРезерва - ПараметрыРасчет["Резервирование" + СокрЛП(НомерСтроки) + "_" + СокрЛП(НомерКвартала-1)];
			СуммаРезерва = СуммаРезерва - ПараметрыРасчет["Расход" + КлючПараметра];
			ПараметрыРасчет["Резерв" + КлючПараметра] = СуммаРезерва;
		КонецЦикла;
		
	КонецЦикла;
	
	//вывод отчета
	Макеты.ОбластьЛист.Параметры.Заполнить(ПараметрыОтчета);
	Макеты.ОбластьЛист.Параметры.Заполнить(ПараметрыРасчет);
	ДокументРезультат.Вывести(Макеты.ОбластьЛист);
	
КонецПроцедуры
