Процедура ЗагрукаУслугИЦен() Экспорт
	
	//параметры
	ТипНоменклатуры=Перечисления.ТипыНоменклатуры.Услуга;
	ЕдиницаИзмерения=Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("796");
	ТипЦенБезНДС=Справочники.ТипыЦенНоменклатуры.НайтиПоКоду("00003");
	ТипЦенСНДС=Справочники.ТипыЦенНоменклатуры.НайтиПоКоду("00001");
	ВалютаРуб=Справочники.Валюты.НайтиПоКоду("643");
	
	ТблСпрУслуг=Новый ТаблицаЗначений;
	ТблСпрУслуг.Колонки.Добавить("Номеклатура");
	ТблСпрУслуг.Колонки.Добавить("Артикул");
	ТблСпрУслуг.Колонки.Добавить("НаименованиеПолное");
	ТблСпрУслуг.Колонки.Добавить("НДС");
	ТблСпрУслуг.Колонки.Добавить("СтавкаНДС");
	ТблСпрУслуг.Колонки.Добавить("ДатаЦены");
	ТблСпрУслуг.Колонки.Добавить("Цена");
	ТблСпрУслуг.Колонки.Добавить("Группа");
	
	//чтение ДБФ
	Сообщить("1 из 3. Чтение файла перечьня УСЛУГ и цен ...");
	DBF = Новый XBase;
	Попытка
		DBF.ОткрытьФайл(СокрЛП(ПутьКФайлам),,Истина);
	Исключение
		Сообщить("    ОШИБКА открытия файла " + СокрЛП(ПутьКФайлам));
		Возврат;
	КонецПопытки;        
	Если НЕ DBF.Открыта() Тогда
		Сообщить("    ОШИБКА открытия файла " + СокрЛП(ПутьКФайлам));
		Возврат;
	КонецЕсли;
	DBF.Кодировка = КодировкаXBase.OEM;
	DBF.ОтображатьУдаленные = Ложь;
	
	DBF.Первая();
	Пока НЕ DBF.ВКонце() Цикл
		//общие
		НовСтрУслуги=ТблСпрУслуг.Добавить();
		НовСтрУслуги.Артикул=СокрЛП(DBF.KODNOM);
		НовСтрУслуги.НаименованиеПолное=СокрЛП(DBF.NAIM);
		НовСтрУслуги.ДатаЦены=DBF.PRICED;
		НовСтрУслуги.Цена=DBF.PRICE;
		НовСтрУслуги.Группа=СокрЛП(DBF.GRUP);
		
		//НДС
		НДСТекст=СокрЛП(DBF.NDS);
		СтавкаНДС=Перечисления.СтавкиНДС.НДС18;
		Если НДСТекст="без НДС" Тогда
			СтавкаНДС=Перечисления.СтавкиНДС.БезНДС;
		КонецЕсли;
		НовСтрУслуги.НДС=НДСТекст;
		НовСтрУслуги.СтавкаНДС=СтавкаНДС;
				
		//шагаем дальше
		DBF.Следующая()
	КонецЦикла;	
	
	//опредление ссылок
	Сообщить("2 из 3. Определение ссылок номенклатуры");
	Для Каждого СтрУслуги Из ТблСпрУслуг Цикл
		НомеклатураСсылка=ОпределитьНоменклатуру(СокрЛП(СтрУслуги.Артикул),СокрЛП(СтрУслуги.НаименованиеПолное),
			ЕдиницаИзмерения,СокрЛП(СтрУслуги.Группа),ТипНоменклатуры,СтрУслуги.СтавкаНДС);
		СтрУслуги.Номеклатура=НомеклатураСсылка;
	КонецЦикла;
	
	//запись цен
	Сообщить("3 из 3. Запись цен");
	ТблСпрУслуг.Сортировать("СтавкаНДС");
	ДокУстановкиЦен=Неопределено;
	СтавкаНДС=Неопределено;
	Для Каждого СтрУслуги Из ТблСпрУслуг Цикл
		//проверки
		Если СтрУслуги.Цена<=1 Тогда
			Продолжить;
		КонецЕсли;
		
		//шапка документа
		Если НЕ СтавкаНДС=СтрУслуги.СтавкаНДС Тогда
			Если НЕ ДокУстановкиЦен=Неопределено Тогда
				ДокУстановкиЦен.Записать(РежимЗаписиДокумента.Запись);
				ДокУстановкиЦен.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			СтавкаНДС=СтрУслуги.СтавкаНДС;
			ДокУстановкиЦен=Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
			ДокУстановкиЦен.Дата=Дата(2001,1,1);
			ДокУстановкиЦен.Комментарий="Загруженно автоматически";
			ДокУстановкиЦен.ТипЦен=?(СтавкаНДС=Перечисления.СтавкиНДС.БезНДС,ТипЦенБезНДС,ТипЦенСНДС);
			ДокУстановкиЦен.Учреждение=Учреждение;
		КонецЕсли;
		
		//ТЧ
		НовСтрТЧ=ДокУстановкиЦен.Материалы.Добавить();
		НовСтрТЧ.Номенклатура=СтрУслуги.Номеклатура;
		НовСтрТЧ.Цена=СтрУслуги.Цена;
		НовСтрТЧ.Валюта=ВалютаРуб;
	КонецЦикла;
	Если НЕ ДокУстановкиЦен=Неопределено Тогда
		ДокУстановкиЦен.Записать(РежимЗаписиДокумента.Запись);
		ДокУстановкиЦен.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	//завершение
	Сообщить("-------------------------------");
	Сообщить("Загрузка завершена!");
	
КонецПроцедуры
