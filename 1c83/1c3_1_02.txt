&НаСервере
Функция ПолучитьДанныеПечати(ИмяМакета, НомерСтроки)
	
	//определение данных по сотруднику
	СтрокаСотрудника = Неопределено;
	Для Каждого СтрокаДанных Из Объект.Сотрудники Цикл
		Если СтрокаДанных.НомерСтроки = НомерСтроки Тогда
			СтрокаСотрудника = СтрокаДанных;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//данные для печати
	ДанныеДляПечати = Новый Структура;
	
	ДанныеДляПечати.Вставить("ДСДата0", Формат(Объект.ДСДата, "ДФ=dd.MM.yyyy"));
	ДанныеДляПечати.Вставить("ДСДата1", Формат(Объект.ДСДата, "ДФ=dd"));
	ДанныеДляПечати.Вставить("ДСДата2", ПолучитьМесяцСтрокой(Объект.ДСДата));
	ДанныеДляПечати.Вставить("ДСДата3", Формат(Объект.ДСДата, "ДФ=yyyy"));
	
	ДанныеДляПечати.Вставить("ДоговорНомер",	СокрЛП(СтрокаСотрудника.ДоговорНомер));
	ДанныеДляПечати.Вставить("ДД0",				Формат(СтрокаСотрудника.ДоговорДата, "ДФ=dd.MM.yyyy"));
	ДанныеДляПечати.Вставить("ДД1",				Формат(СтрокаСотрудника.ДоговорДата, "ДФ=dd"));
	ДанныеДляПечати.Вставить("ДД2",				ПолучитьМесяцСтрокой(СтрокаСотрудника.ДоговорДата));
	ДанныеДляПечати.Вставить("ДД3",				Формат(СтрокаСотрудника.ДоговорДата, "ДФ=yyyy"));
	
	ДанныеДляПечати.Вставить("ФИО",			СокрЛП(СтрокаСотрудника.ФИО));
	ДанныеДляПечати.Вставить("Род1",		СокрЛП(?(СтрокаСотрудника.Сотрудник.ФизическоеЛицо.Пол = Перечисления.ПолФизическогоЛица.Мужской, "ый", "ая")));
	ДанныеДляПечати.Вставить("Род2",		СокрЛП(?(СтрокаСотрудника.Сотрудник.ФизическоеЛицо.Пол = Перечисления.ПолФизическогоЛица.Мужской, "", "а")));
	ДанныеДляПечати.Вставить("ИОФамилия",	СокрЛП(СтрокаСотрудника.ИОФамилия));
	ДанныеДляПечати.Вставить("Адрес",		СокрЛП(СтрокаСотрудника.Адрес));
	
	ДанныеДляПечати.Вставить("ПаспортС",			СокрЛП(СтрокаСотрудника.ПаспортСерия));
	ДанныеДляПечати.Вставить("ПаспортН",			СокрЛП(СтрокаСотрудника.ПаспортНомер));
	ДанныеДляПечати.Вставить("ПаспортДатаВыдачи",	Формат(СтрокаСотрудника.ПаспортДатаВыдачи, "ДФ=dd.MM.yyyy"));
	ДанныеДляПечати.Вставить("ПаспортВыдан",		СокрЛП(СтрокаСотрудника.ПаспортВыдан));
	ДанныеДляПечати.Вставить("ПаспортКод",			СокрЛП(СтрокаСотрудника.ПаспортКод));
	
	ИспытательныйСрокТекст = "____ месяцев";
	Если СтрокаСотрудника.ИспытательныйСрок = 1 Тогда
		ИспытательныйСрокТекст = "один месяц";
	ИначеЕсли СтрокаСотрудника.ИспытательныйСрок = 2 Тогда
		ИспытательныйСрокТекст = "два месяца";
	ИначеЕсли СтрокаСотрудника.ИспытательныйСрок = 3 Тогда
		ИспытательныйСрокТекст = "три месяца";
	ИначеЕсли СтрокаСотрудника.ИспытательныйСрок = 4 Тогда
		ИспытательныйСрокТекст = "четыре месяца";
	ИначеЕсли СтрокаСотрудника.ИспытательныйСрок = 5 Тогда
		ИспытательныйСрокТекст = "пять месяцев";
	ИначеЕсли СтрокаСотрудника.ИспытательныйСрок = 6 Тогда
		ИспытательныйСрокТекст = "шесть месяцев";
	КонецЕсли;
	ДанныеДляПечати.Вставить("ИспытательныйСрок", ИспытательныйСрокТекст);
	
	ДанныеДляПечати.Вставить("Подразделение",		СокрЛП(СтрокаСотрудника.Подразделение.Наименование));
	ДанныеДляПечати.Вставить("Должность",			СокрЛП(СтрокаСотрудника.Должность.Наименование));
	
	//данные по окладу
	ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
	ОкладТекст = РаботаСКурсамиВалют.СформироватьСуммуПрописью(СтрокаСотрудника.Оклад, ВалютаУчета, Истина);
	ОкладТекст = СтрЗаменить(ОкладТекст, "рублей", "");
	ОкладТекст = СокрЛП(ОкладТекст);
	
	ДанныеДляПечати.Вставить("Оклад",		Формат(СтрокаСотрудника.Оклад, "ЧДЦ=2; ЧГ=3,0"));
	ДанныеДляПечати.Вставить("ОкладТекст",	ОкладТекст);
	
	//структура для типовых процедур
	ТипыМакетов = Новый Соответствие;
	ТипыМакетов.Вставить(ИмяМакета, "DOC");
	
	ДвоичныеДанныеМакетов = Новый Соответствие;
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	Макет = ОбъектОбработка.ПолучитьМакет(ИмяМакета);
	ДвоичныеДанныеМакетов.Вставить(ИмяМакета, Макет);
	
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок0", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок1", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок2", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок3", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок4", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок5", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок6", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок7", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок8", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок9", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок10","Общая");
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Блок1безСОУТ", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "БлокРазрыв",	"Общая");
	
	Макеты = Новый Структура;
	Макеты.Вставить("ОписаниеОбластей",		ОписаниеОбластей);
	Макеты.Вставить("ТипыМакетов",			ТипыМакетов);
	Макеты.Вставить("ДвоичныеДанныеМакетов",ДвоичныеДанныеМакетов);
	
	МакетИДанныеОбъекта = Новый Структура;
	МакетИДанныеОбъекта.Вставить("ДанныеДляПечати",				ДанныеДляПечати);
	МакетИДанныеОбъекта.Вставить("Макеты",						Макеты);
	МакетИДанныеОбъекта.Вставить("ЛокальныйКаталогФайловПечати",Неопределено);
	
	//завершение
	Возврат МакетИДанныеОбъекта;
	
КонецФункции
