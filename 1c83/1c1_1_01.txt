общий модуль - ОФК_ОбщегоНазначенияСервер


#Область ПроверкаНаДатуЗапретаРедактирования

// Процедура анализирует режим открытия формы
Процедура ОбъектПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	//проверка
	ОтказТест = Ложь;
	ПроверкаЗаписиДокумента(ОтказТест, ТекущийОбъект, Ложь);
	
	Если ОтказТест = Истина Тогда
		Форма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет разрешено ли записывать изменения в документе в соответсвии с датой запрета редактирования.
//		Для документов ОФК, для подключения типовых процедур их нужно доработать.
Процедура ПроверкаЗаписиДокумента(Отказ, ТекущийОбъект, ВыводитьСообщение = Истина) Экспорт
	
	//определим дату запрета
	ДатаЗапрета = Дата(1, 1, 1);
	
	ОтборПользователь = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей;
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	Если НЕ ТекущийПользователь.Пустая() Тогда
		Если СокрЛП(ТекущийПользователь.Наименование) = "Admin"
			ИЛИ СокрЛП(ТекущийПользователь.Наименование) = "Евгения Золотухина"
			ИЛИ СокрЛП(ТекущийПользователь.Наименование) = "Игорь Дебов" Тогда
			ОтборПользователь = ТекущийПользователь;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтборПользователь", ОтборПользователь);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапрета) КАК ДатаЗапрета
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|ГДЕ
	|	ДатыЗапретаИзменения.Пользователь = &ОтборПользователь";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТаблицаДат.Количество() > 0 Тогда
		Если НЕ ТаблицаДат[0].ДатаЗапрета = Null И НЕ ТаблицаДат[0].ДатаЗапрета = Неопределено Тогда
			ДатаЗапрета = ТаблицаДат[0].ДатаЗапрета;
		КонецЕсли;
	КонецЕсли;
	
	//проверка документа (по периоду регистрации)
	Если ТекущийОбъект.ПериодРегистрации <= КонецДня(ДатаЗапрета) Тогда
		
		Если ВыводитьСообщение = Истина Тогда
			ТекстСообщения = НСтр("ru = '%1 невозможно поместить в запрещенный период'");
			ТекстСообщения = ТекстСообщения + " " + НСтр("ru = '(установлена общая дата запрета).'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", СокрЛП(ТекущийОбъект.Ссылка));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

....................

#Область Документ_БольничныйЛист

//--> ОФК Дебов Игорь 28.12.2016
Процедура УточнитьПериодРасчетаДанныхФСС(Объект) Экспорт
	
	//проверка выводить сообщения или нет
	ВыводитьСообщение = Ложь;
	Если Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленнойЗарплатыРасширенная,ПолныеПрава,ЧтениеНачисленнойЗарплатыРасширенная", , Ложь) Тогда
		ВыводитьСообщение = Истина;
	КонецЕсли;
	
	//расчет предыдущего периода
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БЛТекущий", 		Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", 	Объект.Организация);
	Запрос.УстановитьПараметр("Сотрудник", 		Объект.Сотрудник);
	Запрос.УстановитьПараметр("ДатаНачала", 	НачалоГода(Объект.ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания", 	КонецМесяца(Объект.ПериодРегистрации));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БольничныйЛист.Ссылка КАК БЛСсылка,
	|	БольничныйЛист.Дата КАК БЛДата,
	|	БольничныйЛист.ПериодРасчетаСреднегоЗаработкаНачало,
	|	БольничныйЛист.ПериодРасчетаСреднегоЗаработкаОкончание,
	|	БольничныйЛист.ПериодРасчетаСреднегоЗаработкаПервыйГод,
	|	БольничныйЛист.ПериодРасчетаСреднегоЗаработкаВторойГод
	|ПОМЕСТИТЬ ВТВыбор
	|ИЗ
	|	Документ.БольничныйЛист КАК БольничныйЛист
	|ГДЕ
	|	БольничныйЛист.Организация = &Организация
	|	И БольничныйЛист.Сотрудник = &Сотрудник
	|	И БольничныйЛист.Дата >= &ДатаНачала
	|	И БольничныйЛист.Дата <= &ДатаОкончания
	|	И БольничныйЛист.Ссылка <> &БЛТекущий
	|	И БольничныйЛист.Проведен = ИСТИНА
	|	И БольничныйЛист.ПометкаУдаления = ЛОЖЬ
	|	И БольничныйЛист.ЯвляетсяПродолжениемБолезни = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВыбор.БЛДата) КАК БЛДата
	|ПОМЕСТИТЬ ВТСрез
	|ИЗ
	|	ВТВыбор КАК ВТВыбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСрез.БЛДата,
	|	ВТВыбор.БЛСсылка,
	|	ЕСТЬNULL(ВТВыбор.ПериодРасчетаСреднегоЗаработкаНачало, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ПериодРасчетаСреднегоЗаработкаНачало,
	|	ЕСТЬNULL(ВТВыбор.ПериодРасчетаСреднегоЗаработкаОкончание, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ПериодРасчетаСреднегоЗаработкаОкончание,
	|	ЕСТЬNULL(ВТВыбор.ПериодРасчетаСреднегоЗаработкаПервыйГод, 0) КАК ПериодРасчетаСреднегоЗаработкаПервыйГод,
	|	ЕСТЬNULL(ВТВыбор.ПериодРасчетаСреднегоЗаработкаВторойГод, 0) КАК ПериодРасчетаСреднегоЗаработкаВторойГод
	|ИЗ
	|	ВТСрез КАК ВТСрез
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВыбор КАК ВТВыбор
	|		ПО ВТСрез.БЛДата = ВТВыбор.БЛДата";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Если ТаблицаДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//корректировка данных периода
	ПоследнийБЛ = ТаблицаДанных[0];
	Если ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаНачало = Дата(1, 1, 1)
		ИЛИ ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаОкончание = Дата(1, 1, 1) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ПериодРасчетаСреднегоЗаработкаНачало = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаНачало
		ИЛИ НЕ Объект.ПериодРасчетаСреднегоЗаработкаОкончание = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаОкончание Тогда
		
		//инф. сообщение
		Если ВыводитьСообщение = Истина Тогда
			ТекстСообщения = НСтр("ru = 'Установлен новый период расчета среднего ФСС (причина: в предыдущем больничном использовался другой период).'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		//замена периода
		Объект.ПериодРасчетаСреднегоЗаработкаНачало = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаНачало;
		Объект.ПериодРасчетаСреднегоЗаработкаОкончание = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаОкончание;
		Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаПервыйГод;
		Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = ПоследнийБЛ.ПериодРасчетаСреднегоЗаработкаВторойГод;
		
	КонецЕсли;
	
КонецПроцедуры
//<-- ОФК Дебов Игорь 28.12.2016

//--> ОФК Дебов Игорь 27.12.2016
Процедура УточнитьИнформациюРасчетаДанныхФСС(Объект, ИнформацияОЗаполненностиДанных) Экспорт
	
	Если ИнформацияОЗаполненностиДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = СокрЛП(ИнформацияОЗаполненностиДанных.Текст);
	
	Если СтрНайти(ТекстСообщения, "Данные о заработке неполные.") > 0 Тогда
		ТекстСообщения = ТекстСообщения + "." + Символы.ПС + "Использованы данные о заработке за ";
		ТекстСообщения = ТекстСообщения + Формат(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод, "ЧДЦ=; ЧГ=0") + ", ";
		ТекстСообщения = ТекстСообщения + Формат(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод, "ЧДЦ=; ЧГ=0") + " г.";
		ИнформацияОЗаполненностиДанных.Текст = Новый ФорматированнаяСтрока(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры
//<-- ОФК Дебов Игорь 27.12.2016

#КонецОбласти

#Область ОткрытиеВнешнихОбъектов

//--> ОФК Дебов Игорь 13.04.2017
Функция ПолучитьПараметрыВыполняемойКоманды(НаименованиеОбъекта, ЭтоОтчеты) Экспорт
	
	//поиск ссылки
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеОбъекта", НаименованиеОбъекта);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка,
	|	ДополнительныеОтчетыИОбработкиКоманды.Идентификатор,
	|	ДополнительныеОтчетыИОбработкиКоманды.ВариантЗапуска,
	|	ДополнительныеОтчетыИОбработкиКоманды.Представление,
	|	ДополнительныеОтчетыИОбработкиКоманды.ПоказыватьОповещение,
	|	ДополнительныеОтчетыИОбработкиКоманды.Модификатор,
	|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка.БезопасныйРежим КАК БезопасныйРежим,
	|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка.Вид КАК Вид
	|ИЗ
	|	Справочник.ДополнительныеОтчетыИОбработки.Команды КАК ДополнительныеОтчетыИОбработкиКоманды
	|ГДЕ
	|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка.Наименование = &НаименованиеОбъекта
	|	И ДополнительныеОтчетыИОбработкиКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И ДополнительныеОтчетыИОбработкиКоманды.Ссылка.ЭтоГруппа = ЛОЖЬ";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаКоманд = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТаблицаКоманд.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	//формирование структуры
	ВыполняемаяКоманда = Новый Структура(
		"Ссылка, Представление, 
		|Идентификатор, ВариантЗапуска, ПоказыватьОповещение, 
		|Модификатор, ОбъектыНазначения, ЭтоОтчет, Вид");
		
	ЗаполнитьЗначенияСвойств(ВыполняемаяКоманда, ТаблицаКоманд[0]);
	ВыполняемаяКоманда.ОбъектыНазначения = Неопределено;
	ВыполняемаяКоманда.ЭтоОтчет = ЭтоОтчеты;
	
	//завершение
	Возврат ВыполняемаяКоманда;
	
КонецФункции
//<-- ОФК Дебов Игорь 13.04.2017

#КонецОбласти

.......................
