&НаСервере
Процедура СоздатьОперацииУчетаНДФЛНаСервере(ВидОперации)
	
	//получение исходной таблицы
	ТаблицаДоходов = Новый ТаблицаЗначений;
	Если ВидОперации = 1 Тогда
		ТаблицаДоходов = Объект.Доходы.Выгрузить();
	ИначеЕсли ВидОперации = 2 Тогда
		ТаблицаДоходов = Объект.ДоходыПоДокументам.Выгрузить();
	КонецЕсли;
	
	//индексы документов
	ТаблицаИндексов = Новый ТаблицаЗначений;
	ТаблицаИндексов.Колонки.Добавить("ФизическоеЛицо");
	ТаблицаИндексов.Колонки.Добавить("ДатаВыплаты");
	ТаблицаИндексов.Колонки.Добавить("КолвоОпераций");
	
	Для Каждого СтрокаДохода Из ТаблицаДоходов Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаДохода.ФизическоеЛицо) ИЛИ НЕ ЗначениеЗаполнено(СтрокаДохода.ДатаВыплаты) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаИндекса = ТаблицаИндексов.Добавить();
		СтрокаИндекса.ФизическоеЛицо = СтрокаДохода.ФизическоеЛицо;
		СтрокаИндекса.ДатаВыплаты = НачалоДня(СтрокаДохода.ДатаВыплаты);
		СтрокаИндекса.КолвоОпераций = 1;
	КонецЦикла;
	
	ТаблицаИндексов.Свернуть("ФизическоеЛицо,ДатаВыплаты", "КолвоОпераций");
	ТаблицаИндексов.Сортировать("ФизическоеЛицо,ДатаВыплаты");
	
	//общие параметры
	СрокПеречисления = Перечисления.СрокиПеречисляемогоНалога.ПрочиеДоходы;
	
	//создание документов (один документ на физ-лицо)
	Для Каждого СтрокаИндекса Из ТаблицаИндексов Цикл
		
		//шапка документа
		НовыйДокумент = Документы.ОперацияНалоговогоУчетаПоНДФЛ.СоздатьДокумент();
		НовыйДокумент.Дата = СтрокаИндекса.ДатаВыплаты;
		НовыйДокумент.ДатаОперации = СтрокаИндекса.ДатаВыплаты;
		НовыйДокумент.Организация = Объект.Организация;
		НовыйДокумент.Сотрудник = СтрокаИндекса.ФизическоеЛицо;
		НовыйДокумент.Ответственный = Объект.Ответственный;
		
		Комментарий = "";
		Если ВидОперации = 1 Тогда
			Комментарий = ?(Объект.ЗагружатьТолькоДокументыПП = Истина, "Только ПП. ", "");
			Комментарий = Комментарий + СокрЛП(Объект.ФайлЗагрузки);
		ИначеЕсли ВидОперации = 2 Тогда
			Комментарий = "Перенос из документов [Начисление прочих доходов]";
		КонецЕсли;
		НовыйДокумент.Комментарий = Комментарий;
		
		//таблица платежек
		ТаблицаПП = Новый ТаблицаЗначений;
		ТаблицаПП.Колонки.Добавить("НомерПП");
		ТаблицаПП.Колонки.Добавить("ДатаПеречисления");
		ТаблицаПП.Колонки.Добавить("СуммаНалога");
		
		//сведения о доходах + НДФЛ удержанный
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ФизическоеЛицо", СтрокаИндекса.ФизическоеЛицо);
		СтруктураПоиска.Вставить("ДатаВыплаты", СтрокаИндекса.ДатаВыплаты);
		НайденныеСтроки = ТаблицаДоходов.НайтиСтроки(СтруктураПоиска);
		Для Каждого СтрокаДохода Из НайденныеСтроки Цикл
			
			//параметры документа
			КодДохода = Справочники.ВидыДоходовНДФЛ.НайтиПоКоду(Формат(СтрокаДохода.КодДохода, "ЧДЦ=; ЧГ=0"));
			СтавкаНалогообложения = КодДохода.СтавкаНалогообложенияРезидента;
			
			Ставка = Перечисления.НДФЛСтавки.Ставка35;
			Если СтавкаНалогообложения = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
				Ставка = Перечисления.НДФЛСтавки.Ставка13;
			КонецЕсли;
			
			КодВычета = Справочники.ВидыВычетовНДФЛ.ПустаяСсылка();
			Если НЕ СтрокаДохода.КодВычета = 0 Тогда
				КодВычета = Справочники.ВидыВычетовНДФЛ.НайтиПоКоду(Формат(СтрокаДохода.КодВычета, "ЧДЦ=; ЧГ=0"));
			КонецЕсли;
			
			СуммаВычета = 0;
			Если СтрокаДохода.КодВычета = 505 Тогда
				СуммаВычета = 4000;
			КонецЕсли;
			
			//заполнение ТЧ документа
			Если Объект.ЗагружатьТолькоДокументыПП = Ложь ИЛИ ВидОперации = 2 Тогда
				
				НоваяСтрока1 = НовыйДокумент.СведенияОДоходах.Добавить();
				НоваяСтрока1.ДатаПолученияДохода = СтрокаДохода.ДатаВыплаты;
				НоваяСтрока1.КодДохода = КодДохода;
				НоваяСтрока1.СуммаДохода = СтрокаДохода.СуммаДохода;
				НоваяСтрока1.КодВычета = КодВычета;
				НоваяСтрока1.СуммаВычета = СуммаВычета;
				Если СтавкаНалогообложения = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35 Тогда
					НоваяСтрока1.СуммаНалогаИсчисленная = СтрокаДохода.СуммаНалога;
				КонецЕсли;
			
				Если НЕ СтрокаДохода.ДатаУдержания = Дата(1,1,1) Тогда
					НоваяСтрока2 = НовыйДокумент.НДФЛУдержанный.Добавить();
					НоваяСтрока2.МесяцНалоговогоПериода = СтрокаДохода.ДатаУдержания;
					НоваяСтрока2.СтавкаНалогообложения = СтавкаНалогообложения;
					НоваяСтрока2.Ставка = Ставка;
					НоваяСтрока2.Сумма = СтрокаДохода.СуммаНалога;
					НоваяСтрока2.КодДохода = КодДохода;
					НоваяСтрока2.СрокПеречисления = СрокПеречисления;
					НоваяСтрока2.СуммаВыплаченногоДохода = СтрокаДохода.СуммаДохода;
				КонецЕсли;
			
				Если СтавкаНалогообложения = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
					НоваяСтрока4 = НовыйДокумент.НДФЛИсчисленныйПоСтавке13.Добавить();
					НоваяСтрока4.МесяцНалоговогоПериода = НачалоМесяца(СтрокаДохода.ДатаВыплаты);
					НоваяСтрока4.Сумма = СтрокаДохода.СуммаНалога;
				КонецЕсли;
				
				Если НЕ КодВычета = Справочники.ВидыВычетовНДФЛ.ПустаяСсылка() Тогда
					НоваяСтрока5 = НовыйДокумент.ПредоставленныеВычеты.Добавить();
					НоваяСтрока5.МесяцНалоговогоПериода = НачалоМесяца(СтрокаДохода.ДатаВыплаты);
					НоваяСтрока5.КодВычета = КодВычета;
					НоваяСтрока5.Сумма = СуммаВычета;
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрокаДохода.СуммаНалогаПП > 0 Тогда
				СтрокаПП = ТаблицаПП.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПП, СтрокаДохода); 
			КонецЕсли;
			
		КонецЦикла;
		
		//перечисление с реквизитами ПП
		ТаблицаПП.Свернуть("НомерПП,ДатаПеречисления", "СуммаНалога");
		Для Каждого СтрокаПП Из ТаблицаПП Цикл
			
			НоваяСтрока3 = НовыйДокумент.НДФЛПеречисленный.Добавить();
			НоваяСтрока3.МесяцНалоговогоПериода = НачалоМесяца(СтрокаПП.ДатаПеречисления);
			НоваяСтрока3.Ставка = Ставка;
			НоваяСтрока3.Сумма = СтрокаПП.СуммаНалога;
			НоваяСтрока3.РеквизитыПлатежногоПоручения = СокрЛП(СтрокаПП.НомерПП);
			
		КонецЦикла;
		
		//запись документа
		ЕстьОшибкаЗаписи = Ложь;
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЕстьОшибкаЗаписи = Истина;
		КонецПопытки;
		
		Если ЕстьОшибкаЗаписи = Ложь И ВидОперации = 1 Тогда
			НайденныеСтрокиДляЗаписи = Объект.Доходы.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаДохода Из НайденныеСтрокиДляЗаписи Цикл
				СтрокаДохода.ДокументОперации = НовыйДокумент.Ссылка;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
