// Функция расчитывает отложенное обязательство на дату
Функция ПолучитьСтруктуруРасчетаОбязательства(Начисление, ПериодВыплаты, ПредыдущиеОбязательство = 0, ПредыдущиеОбязательствоСВ = 0, ВидПоказателей = "") Экспорт
	
	//описание структуры
	СтруктураРасчета = Новый Структура;
	СтруктураРасчета.Вставить("Обязательство", 0);
	СтруктураРасчета.Вставить("ОбязательствоСВ", 0);
	СтруктураРасчета.Вставить("ПредыдущиеОбязательство", 0);
	СтруктураРасчета.Вставить("ПредыдущиеОбязательствоСВ", 0);
	СтруктураРасчета.Вставить("ПроцентныйРасход", 0);
	СтруктураРасчета.Вставить("ПроцентныйРасходСВ", 0);
	СтруктураРасчета.Вставить("ПроцентныйРасходПроцент", 0);
	СтруктураРасчета.Вставить("ПроцентныйРасходПроцентСВ", 0);
	
	//расчет основной
	КоэффициентТекучестиКадров = Начисление["КоэффициентТекучестиКадров" + ВидПоказателей] / 100;
	ДО1 = Pow(1 - КоэффициентТекучестиКадров, ПериодВыплаты);
	КоэффициентВероятностиДожития = Начисление["КоэффициентВероятностиДожития" + ВидПоказателей];
	ДО2 = Pow(1 - КоэффициентВероятностиДожития, ПериодВыплаты);
	СтавкаДисконтирования = Начисление["СтавкаДисконтирования" + ВидПоказателей] / 100;
	ДО3 = Pow(1 + СтавкаДисконтирования, ПериодВыплаты);
		
	СтруктураРасчета.Вставить("Обязательство", Окр(Начисление.БазоваяВеличина * ДО1 * ДО2 / ДО3, 2));
	СтруктураРасчета.Вставить("ОбязательствоСВ", Окр((Начисление.БазоваяВеличинаИтог - Начисление.БазоваяВеличина) * ДО1 * ДО2 / ДО3, 2));
	
	//расчет дополнительный
	Если НЕ ПредыдущиеОбязательство = 0 И НЕ СтруктураРасчета.Обязательство = 0 Тогда
		СтруктураРасчета.Вставить("ПредыдущиеОбязательство", ПредыдущиеОбязательство);
		ПроцентныйРасход = СтруктураРасчета.Обязательство - ПредыдущиеОбязательство;
		СтруктураРасчета.Вставить("ПроцентныйРасход", ПроцентныйРасход);
		ПроцентныйРасходПроцент = Окр(ПроцентныйРасход / СтруктураРасчета.Обязательство, 2) * 100;
		СтруктураРасчета.Вставить("ПроцентныйРасходПроцент", ПроцентныйРасходПроцент);
	КонецЕсли;
	
	Если НЕ ПредыдущиеОбязательствоСВ = 0 И НЕ СтруктураРасчета.ОбязательствоСВ = 0 Тогда
		СтруктураРасчета.Вставить("ПредыдущиеОбязательствоСВ", ПредыдущиеОбязательствоСВ);
		ПроцентныйРасходСВ = СтруктураРасчета.ОбязательствоСВ - ПредыдущиеОбязательствоСВ;
		СтруктураРасчета.Вставить("ПроцентныйРасходСВ", ПроцентныйРасходСВ);
		ПроцентныйРасходПроцентСВ = Окр(ПроцентныйРасходСВ / СтруктураРасчета.ОбязательствоСВ, 2) * 100;
		СтруктураРасчета.Вставить("ПроцентныйРасходПроцентСВ", ПроцентныйРасходПроцентСВ);
	КонецЕсли;
	
	//завершение
	Возврат СтруктураРасчета;
	  
КонецФункции
