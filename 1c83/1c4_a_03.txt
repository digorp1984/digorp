//-----------------------------------------------
// 4. Расчет премии
//-----------------------------------------------

Процедура ВыполнитьРасчетПремии() Экспорт
	
	//общие параметры
	КлючПР=ПолучитьКлючПериодаРасчета();
	ДокТабель=ПоискСохраненногоТабеля(КлючПР);
	ДокРН=ПоискРасчетаПремии(КлючПР);
	
	//проверка на ошибки
	Если ДокТабель=Документы.ВводИндивидуальныхГрафиковРаботыОрганизации.ПустаяСсылка() Тогда
		Сообщить("Не найден заполненный табель за выбранный период - "+СокрЛП(ПериодРегистрации));
		Возврат;
	КонецЕсли;
	
	//расчет
	ЗапросРасчет=Новый Запрос;
	ЗапросРасчет.Текст=ПолучитьЗапросРасчета();
	ЗапросРасчет.УстановитьПараметр("ДокТабель",ДокТабель);
	ЗапросРасчет.УстановитьПараметр("Учреждение",Учреждение);
	ЗапросРасчет.УстановитьПараметр("Свойство",ПолучитьСвойствоРасширенияЗоны());
	ЗапросРасчет.УстановитьПараметр("ПериодРегистрации",НачалоМесяца(ПериодРегистрации));
	ЗапросРасчет.УстановитьПараметр("КонПериод",КонецМесяца(ПериодРегистрации));
	ЗапросРасчет.УстановитьПараметр("МассивВР",СобратьМассивВРБазы());
	ТблРасчет=ЗапросРасчет.Выполнить().Выгрузить();
	
	КолвоИтогов=0;
	Для Каждого СтрРасчет Из ТблРасчет Цикл
		СтрРасчет.НормаМес=СтрРасчет.КолвоДней*СтрРасчет.НормаДень;
		СтрРасчет.БазаЕд=Окр(?(СтрРасчет.НормаМес=0,0,СтрРасчет.СуммаБазы/СтрРасчет.НормаМес),2);
		СтрРасчет.Итого=(СтрРасчет.ФактМес-СтрРасчет.НормаМес)*СтрРасчет.БазаЕд;
		Если СтрРасчет.Итого<>0 Тогда
			КолвоИтогов=КолвоИтогов+1;
		КонецЕсли;
		
		Сообщить("Сотрудник - "+СокрЛП(СтрРасчет.Сотрудник));
		Сообщить("      Кол-во дней: "+СокрЛП(СтрРасчет.КолвоДней));
		Сообщить("      Норма за день: "+СокрЛП(СтрРасчет.НормаДень));
		Сообщить("      Норма за месяц: "+СокрЛП(СтрРасчет.НормаМес));
		Сообщить("      Сумма базы: "+СокрЛП(СтрРасчет.СуммаБазы));
		Сообщить("      База за ед.: "+СокрЛП(СтрРасчет.БазаЕд));
		Сообщить("      За месяц по табелю: "+СокрЛП(СтрРасчет.ФактМес));
		Сообщить("      Итого: "+СокрЛП(СтрРасчет.Итого));
	КонецЦикла;
	Если КолвоИтогов=0 Тогда
		Сообщить("За указанный период  - НЕТ расчитанных премий!");
		Возврат;
	КонецЕсли;
	
	//сохранение расчета
	Если ДокРН=Документы.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ПустаяСсылка() Тогда
		//создаем новый документ
		НовДок=Документы.РегистрацияРазовыхНачисленийРаботниковОрганизаций.СоздатьДокумент();
		НовДок.Дата=КонецМесяца(ПериодРегистрации);
		НовДок.ПериодРегистрации=ПериодРегистрации;
		НовДок.Организация=Учреждение;
		НовДок.Комментарий=КлючПР+" Документ создан автоматически при расчете - расширения зоны обслуживания";
		НовДок.СпособРегистрацииВремени=Перечисления.СпособыРегистрацииВремени.РегистрацияДляЦелойСмены;
		НовДок.КОСГУ=КОСГУ;
		НовДок.СтатьяРасходов=СтатьяРасходов;
		НовДок.СтатьяФинансирования=СтатьяФинансирования;
		НовДок.ИныеДоходы=Ложь;
		НовДок.ВыплачиватьМежрасчетно=Ложь;
		ЗаполнитьТЧДокумента(НовДок, ТблРасчет);
		НовДок.Записать(РежимЗаписиДокумента.Запись);
		ДокументПремии=НовДок.Ссылка;
		
	Иначе
		//правим существующий
		ДокументПремии=ДокРН.Ссылка;
		ДокОбъект=ДокументПремии.ПолучитьОбъект();
		ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		ДокОбъект.ДополнительныеНачисления.Очистить();
		ДокОбъект.ДополнительныеНачисленияПоИФ.Очистить();
		ДокОбъект.ФизическиеЛица.Очистить();
		ЗаполнитьТЧДокумента(ДокОбъект, ТблРасчет);
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
	//завершение
	ФормаДокумента=ДокументПремии.ПолучитьФорму();
	ФормаДокумента.Открыть();
	
КонецПроцедуры
