//-----------------------------------------------
// 3. Ввод табелей
//-----------------------------------------------

Процедура ЗагрузитьИлиСформироватьТабель() Экспорт
	
	//общие параметры
	КлючПР=ПолучитьКлючПериодаРасчета();
	ДокТабеля=ПоискСохраненногоТабеля(КлючПР);
	
	//обработка табеля
	Если НЕ ДокТабеля=Документы.ВводИндивидуальныхГрафиковРаботыОрганизации.ПустаяСсылка() Тогда
		//загрузка
		ТблТабельСохраненный=ДокТабеля.ГрафикРаботы.Выгрузить();
		Табель.Загрузить(ТблТабельСохраненный);
		
	Иначе
		//создание нового
		ЗапросНовыйТабель=Новый Запрос;
		ЗапросНовыйТабель.Текст="ВЫБРАТЬ
		                        |	СотрудникиОрганизаций.Ссылка КАК Сотрудник,
		                        |	СотрудникиОрганизаций.Наименование КАК ФИО,
		                        |	ЗначенияСвойствОбъектов.Значение КАК НормаЗаДень
		                        |ИЗ
		                        |	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
		                        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		                        |		ПО СотрудникиОрганизаций.Ссылка = ЗначенияСвойствОбъектов.Объект
		                        |ГДЕ
		                        |	ЗначенияСвойствОбъектов.Свойство = &Свойство
		                        |	И ЗначенияСвойствОбъектов.Значение > 0
		                        |
		                        |УПОРЯДОЧИТЬ ПО
		                        |	ФИО";
		ЗапросНовыйТабель.УстановитьПараметр("Свойство",ПолучитьСвойствоРасширенияЗоны());
		ТблНовыйТабель=ЗапросНовыйТабель.Выполнить().Выгрузить();
		Табель.Загрузить(ТблНовыйТабель);
	КонецЕсли;
	
	//установка выборки норм
	ЗапросНормы=Новый Запрос;
	ЗапросНормы.Текст="ВЫБРАТЬ
	                  |	ЗначенияСвойствОбъектов.Объект КАК Сотрудник,
	                  |	ЗначенияСвойствОбъектов.Значение КАК НормаЗаДень
	                  |ИЗ
	                  |	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	                  |ГДЕ
	                  |	ЗначенияСвойствОбъектов.Свойство = &Свойство
	                  |
	                  |УПОРЯДОЧИТЬ ПО
	                  |	Сотрудник";
	ЗапросНормы.УстановитьПараметр("Свойство",ПолучитьСвойствоРасширенияЗоны());
	ВыборкаНормы=ЗапросНормы.Выполнить().Выбрать();
	
КонецПроцедуры

Процедура СохранитьТабель() Экспорт
	
	//общие параметры
	КлючПР=ПолучитьКлючПериодаРасчета();
	ДокТабеля=ПоискСохраненногоТабеля(КлючПР);
	ТблТабель=Табель.Выгрузить();
	
	//проверка
	Если Табель.Количество()=0 Тогда
		Сообщить("Табель НЕ сохранен, т.к. он не заполнен!");
		Возврат;
	КонецЕсли;
		
	//сохранение
	Если НЕ ДокТабеля=Документы.ВводИндивидуальныхГрафиковРаботыОрганизации.ПустаяСсылка() Тогда
		//в уже созданный документ
		ДокОбъект=ДокТабеля.Получитьобъект();
		ДокОбъект.ГрафикРаботы.Очистить();
		ДокОбъект.ГрафикРаботы.Загрузить(ТблТабель);
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
		
	Иначе
		//в новый документ
		ДокНовый=Документы.ВводИндивидуальныхГрафиковРаботыОрганизации.СоздатьДокумент();
		ДокНовый.Дата=ТекущаяДата();
		ДокНовый.Организация=Учреждение;
		ДокНовый.ПериодРегистрации=ПериодРегистрации;
		ДокНовый.СпособВводаДанных=Перечисления.СпособыВводаДанныхОВремени.ПоДням;
		ДокНовый.Комментарий=КлючПР+" Документ создан автоматически при расчете - расширения зоны обслуживания";
		ДокНовый.ГрафикРаботы.Загрузить(ТблТабель);
		ДокНовый.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры
