&НаКлиенте
Процедура ЧтениеНовыхФизическихЛиц()
	
	//загрузка из ексел
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.Visible = False;
	    Excel.DisplayAlerts = False;
	Исключение
		ТекстСообщения = НСтр("ru = 'ОШИБКА: Не удалось подключить COMОбъект.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	Попытка
		Workbook = Excel.Workbooks.Open(СокрЛП(Объект.ФайлЗагрузки),,Истина);
	Исключение
		Excel.Quit();
		Excel = Неопределено;
		ТекстСообщения = НСтр("ru = 'ОШИБКА: Ошибка при открытии файла.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
    Лист = Workbook.Sheets(Объект.НомерСтраницы);
    КолвоКолонок = Лист.Cells(1,1).SpecialCells(11).Column;
    КолвоСтрок   = Лист.Cells(1,1).SpecialCells(11).Row;
	НачСтрока = 4;
	КонСтрока = 0;
	
    НачСтрока = ?(НачСтрока = 0, 1, НачСтрока);
    КонСтрока = ?(КонСтрока = 0, КолвоСтрок, КонСтрока);
	КонСтрока = Мин(КонСтрока, КолвоСтрок);
	
    Массив = Лист.Range(Лист.Cells(НачСтрока, 1), Лист.Cells(КонСтрока, КолвоКолонок)).Value; // тип COMSafeArray
    КолвоСтрок = Массив.GetUpperBound(1);

	Для НомерСтроки = 1 По КолвоСтрок Цикл
		
		//основные данные физ-лица
		ФИО = СокрЛП(Массив.GetValue(4, НомерСтроки));
		ДатаРождения = Массив.GetValue(5, НомерСтроки);
		НужноДобавить = Истина;
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ФИО", ФИО);
		СтруктураПоиска.Вставить("ДатаРождения", ДатаРождения);
		
		//проверка
		НайденныеСтроки = Объект.Доходы.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Если ЗначениеЗаполнено(НайденныеСтроки[0].ФизическоеЛицо) Тогда
				//есть ссылка по этому физ-лицу
				НужноДобавить = Ложь;
			КонецЕсли;
		Иначе
			//этого физ-лица нет в списке загружаемых доходов
			НужноДобавить = Ложь;
		КонецЕсли;
		
		НайденныеСтроки = Объект.НовыеФизическиеЛица.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			//значит уже есть в списке новых физ-лиц
			НужноДобавить = Ложь;
		КонецЕсли;
		
		Если НужноДобавить = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			
			//общие данные
			НоваяСтрока = Объект.НовыеФизическиеЛица.Добавить();
			НоваяСтрока.ФИО = ФИО;
			НоваяСтрока.ДатаРождения = ДатаРождения;
			НоваяСтрока.МестоРождения = СокрЛП(Массив.GetValue(6,НомерСтроки));
			НоваяСтрока.Пол = СокрЛП(Массив.GetValue(8,НомерСтроки));
			
			//инн
			ИНН = Массив.GetValue(3,НомерСтроки);
			Если ТипЗнч(ИНН) = Тип("Число") Тогда
				НоваяСтрока.ИНН = Формат(ИНН, "ЧДЦ=; ЧГ=0");
			Иначе
				НоваяСтрока.ИНН = СокрЛП(ИНН);
			КонецЕсли;
		
			//паспорт
			НоваяСтрока.ДатаВыдачи = Массив.GetValue(11,НомерСтроки);
			НоваяСтрока.Номер =  СтрЗаменить(СокрЛП(Массив.GetValue(14,НомерСтроки)), " ", "");
			НоваяСтрока.Серия =  СтрЗаменить(СокрЛП(Массив.GetValue(17,НомерСтроки)), " ", "");
			Если СтрДлина(НоваяСтрока.Серия) <> 4 Тогда
				ТекстСообщения = "Количество цифр в серии паспорта не равно 4: " + строка(НоваяСтрока.ФИО) + " строка " + Строка(НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Иначе
				НоваяСтрока.Серия = Лев(НоваяСтрока.Серия,2) + " " + Прав(НоваяСтрока.Серия,2);
			КонецЕсли;	
		
			НоваяСтрока.КемВыдан = СокрЛП(Массив.GetValue(12,НомерСтроки));
			НоваяСтрока.КодПодразделения = СокрЛП(Массив.GetValue(13,НомерСтроки));
			НоваяСтрока.КодДокумента = СокрЛП(Массив.GetValue(15,НомерСтроки));
		
		    //адрес
			НоваяСтрока.Страна = СокрЛП(Массив.GetValue(19,НомерСтроки));
			НоваяСтрока.Индекс = СокрЛП(Массив.GetValue(20,НомерСтроки));
			НоваяСтрока.Регион = СокрЛП(Массив.GetValue(21,НомерСтроки));
			НоваяСтрока.ТипРайон = СокрЛП(Массив.GetValue(22,НомерСтроки));
			НоваяСтрока.Район = СокрЛП(Массив.GetValue(23,НомерСтроки));
	        НоваяСтрока.ТипГород = СокрЛП(Массив.GetValue(24,НомерСтроки));
	        НоваяСтрока.Город = СокрЛП(Массив.GetValue(25,НомерСтроки));
			НоваяСтрока.ТипПункт = СокрЛП(Массив.GetValue(26,НомерСтроки));
			НоваяСтрока.НаселенныйПункт = СокрЛП(Массив.GetValue(27,НомерСтроки));
			НоваяСтрока.ТипУлица = СокрЛП(Массив.GetValue(28,НомерСтроки));
			НоваяСтрока.Улица = СокрЛП(Массив.GetValue(29,НомерСтроки));
			НоваяСтрока.Дом = СокрЛП(Массив.GetValue(30,НомерСтроки));
			НоваяСтрока.Корпус = СокрЛП(Массив.GetValue(31,НомерСтроки));
			НоваяСтрока.Строение = СокрЛП(Массив.GetValue(32,НомерСтроки));
			НоваяСтрока.Квартира = СокрЛП(Массив.GetValue(33,НомерСтроки));
			НоваяСтрока.АдресПредставление = СокрЛП(Массив.GetValue(34,НомерСтроки));
			
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка чтения строки: 1%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "1%", СокрЛП(НомерСтроки));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	//завершение
	Лист = Неопределено;
	Workbook = Неопределено;
	Excel.Quit();
	Excel = Неопределено;
	
	Если Объект.НовыеФизическиеЛица.Количество() > 0 Тогда
		Элементы.Группа1.ТекущаяСтраница = Элементы.Группа6
	КонецЕсли;
	
КонецПроцедуры
