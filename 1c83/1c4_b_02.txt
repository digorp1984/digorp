#Область Выгрузка

Процедура СформироватьФаилЕкселПоФОТ(ТаблицаДанных, МассивРассылки, ДатаДанных)
	
	//параметры
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОФК_РассылкаИАнализКадровогоФОТ";
	ТабДокумент.КлючПараметровПечати = "КЛЮЧ_ПАРАМЕТРОВ_ПЕЧАТИ_ОФК_РассылкаИАнализКадровогоФОТ";
	
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ПолеСверху = 20;
	ТабДокумент.ПолеСнизу = 10;
	ТабДокумент.ПолеСлева = 25;
	ТабДокумент.ПолеСправа = 10;
	ТабДокумент.РазмерКолонтитулаСнизу = 0;
	ТабДокумент.РазмерКолонтитулаСверху = 0;
	
	Макет = ПолучитьМакет("ШаблонФайлаЕксел");
	
	СтруктураОбластей = Новый Структура;
	СтруктураОбластей.Вставить("ОбластьШапка",				Макет.ПолучитьОбласть("ОбластьШапка"));
	СтруктураОбластей.Вставить("ОбластьСтрока",				Макет.ПолучитьОбласть("ОбластьСтрока"));
	СтруктураОбластей.Вставить("ОбластьСтрокаРасхождение",	Макет.ПолучитьОбласть("ОбластьСтрокаРасхождение"));
	СтруктураОбластей.Вставить("ОбластьПодвал",				Макет.ПолучитьОбласть("ОбластьПодвал"));
	СтруктураОбластей.Вставить("ОбластьГруппа",				Макет.ПолучитьОбласть("ОбластьГруппа"));
	
	//вывод шапки
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ДатаДанных", Формат(ДатаДанных, "ДФ=dd.MM.yyyy"));
	СтруктураОбластей.ОбластьШапка.Параметры.Заполнить(СтруктураПараметров);
	ТабДокумент.Вывести(СтруктураОбластей.ОбластьШапка);
	
	//вывод групп + строк
	ТекущаяОрганизация = Неопределено;
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		//группа
		Если НЕ ТекущаяОрганизация = СтрокаДанных.Организация Тогда
			
			ТекущаяОрганизация = СтрокаДанных.Организация;
			
			СтруктураПараметров = Новый Структура();
			СтруктураПараметров.Вставить("Организация", СокрЛП(ТекущаяОрганизация));
			СтруктураОбластей.ОбластьГруппа.Параметры.Заполнить(СтруктураПараметров);
			ТабДокумент.Вывести(СтруктураОбластей.ОбластьГруппа);
			
		КонецЕсли;
		
		//строка
		Если СтрокаДанных.ЕстьОшибка = Ложь Тогда
			СтруктураОбластей.ОбластьСтрока.Параметры.Заполнить(СтрокаДанных);
			ТабДокумент.Вывести(СтруктураОбластей.ОбластьСтрока);
		Иначе
			СтруктураОбластей.ОбластьСтрокаРасхождение.Параметры.Заполнить(СтрокаДанных);
			ТабДокумент.Вывести(СтруктураОбластей.ОбластьСтрокаРасхождение);
		КонецЕсли;
		
	КонецЦикла;
	
	//вывод подвала
	ТабДокумент.Вывести(СтруктураОбластей.ОбластьПодвал);
	
	//запишем данные в фаил
	ОшибкаЗаписи = Ложь;
	ТекущиеВремя = ТекущаяДата();
	ИмяФайла = ПолучитьИмяВременногоФайла(".xlsx");
	
	ИмяФайлаПредставление = Формат(ДатаДанных, "ДФ=yyyy") + "_" + Формат(ДатаДанных, "ДФ=MM") + "_" + Формат(ДатаДанных, "ДФ=dd") + "_";
	ИмяФайлаПредставление = ИмяФайлаПредставление + СтрЗаменить(Формат(ТекущиеВремя, "ДЛФ=T"), ":", "_");
	ИмяФайлаПредставление = ИмяФайлаПредставление + ".xlsx";
	
	Попытка
		ТабДокумент.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
	Исключение
		ОшибкаЗаписи = Истина;
		Инфо = ИнформацияОбОшибке();
	КонецПопытки;
	
	//запишем результат в структуру рассылки
	Для Каждого СтруктураПараметров Из МассивРассылки Цикл
		
		СтруктураПараметров.Вставить("ИмяФайла",	ИмяФайла);
		СтруктураПараметров.Вставить("ИмяФайла2",	ИмяФайлаПредставление);
		СтруктураПараметров.Вставить("Ошибка",		ОшибкаЗаписи);
		
		Если ОшибкаЗаписи = Истина Тогда
			//запишем данные об ошибке
			СтруктураПараметров.Вставить("ТелоПисьма", СокрЛП(Инфо.Описание) + Символы.ПС + СокрЛП(Инфо.Причина.Описание));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
