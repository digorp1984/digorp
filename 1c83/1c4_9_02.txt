//ВЫГРУЗКА данных для программы ШТРИХ-КОД
Процедура КнопкаВыполнитьНажатие1(Кнопка)
	
	ОчиститьСообщения();
	Сообщить("1 из 9. Установка начальных параметров, "+СокрЛП(ТекущаяДата()));
		
	//------------- Заполнение начальных параметров -------------------------
	ФаилПуть=ПутьКаталога;
	Если (Прав(ФаилПуть, 1) <> "\") Тогда
		ФаилПуть=ФаилПуть+"\";
	КонецЕсли;
	ПутьЗаписи=ФаилПуть+"UNI_ASBC\";
	ПутьЗагрузки=ФаилПуть+"ASBC_UNI\";
	СуммаБаланса=0;
	ЭтотОбъект.СуммаКонтрол=0;
	Счет21=ПланыСчетов.ЕПСБУ.НайтиПоКоду("21");
	
	тМОЛ = Новый ТекстовыйДокумент;
	тНОМ = Новый ТекстовыйДокумент;
	тПОМ = Новый ТекстовыйДокумент;
	тПОД = Новый ТекстовыйДокумент;
	тСЧТ = Новый ТекстовыйДокумент;
	тИНВ = Новый ТекстовыйДокумент;
	тПСК = Новый ТекстовыйДокумент;
	тПГК = Новый ТекстовыйДокумент;
	тМОД = Новый ТекстовыйДокумент;
	
	зПОМ = Новый ТаблицаЗначений;
	зПОМ.Колонки.Добавить("УКЗ");
	зПОМ.Колонки.Добавить("ПНаим");
	зПОМ.Колонки.Добавить("Наим");//только цифры
	зМОЛ = Новый ТаблицаЗначений;
	зМОЛ.Колонки.Добавить("УКЗ");
	зМОЛ.Колонки.Добавить("ПНаим");
	зМОЛ.Колонки.Добавить("Наим");
	зНОМ = Новый ТаблицаЗначений;
	зНОМ.Колонки.Добавить("УКЗ");
	зНОМ.Колонки.Добавить("ПНаим");
	зНОМ.Колонки.Добавить("Наим");
	зНОМ.Колонки.Добавить("ЕдИзм");
	зСЧТ = Новый ТаблицаЗначений;
	зСЧТ.Колонки.Добавить("УКЗ");
	зСЧТ.Колонки.Добавить("ПНаим");
	зСЧТ.Колонки.Добавить("НомСч");
		
	//------------- Выгрузка данных -----------------------------------------
	//помещение по умолчанию - выбранное учреждение
	ИДПомПоУмолчанию="00001";
	ИДПом=1;
	НовСтрПОМ=зПОМ.Добавить();
	НовСтрПОМ.УКЗ=ИДПомПоУмолчанию;
	НовСтрПОМ.ПНаим=СокрЛП(Организация.НаименованиеПолное);
	НовСтрПОМ.Наим=ИДПомПоУмолчанию;
	
	//Формирование справочника Подразделения - POD.txt
	Сообщить("2 из 9. Формирование файла - POD.txt, "+СокрЛП(ТекущаяДата()));
	СправПодр=Справочники.Подразделения.Выбрать();
	Пока СправПодр.Следующий() Цикл
		Если (СправПодр.ПометкаУдаления) Или (СправПодр.ЭтоГруппа) Тогда
			Продолжить;
		КонецЕсли;
		Стр=СокрЛП(СправПодр.Код)+"|";
		Стр=Стр+КонтрольДлиныПоля(СправПодр.Наименование, 160)+"|";
		Стр=Стр+КонтрольДлиныПоля(СправПодр.Наименование, 30)+"|";
		тПОД.ДобавитьСтроку(Стр); Стр="";
	КонецЦикла;
	
	//Формирование инвентарных карточек  - INV.txt
	//Документы выбираются на основание бухгалтерсих остатков по ОС,
	//а дополнительная информация на основании регистров - МестонахождениеОС и СостояниеОС
	Сообщить("3 из 9. Расчет основных данных ..., "+СокрЛП(ТекущаяДата()));
	ОССчетЗабаланса=Ложь;
	Если (Счет.Родитель.Родитель=Счет21) Или (Счет.Родитель=Счет21) Или (Счет=Счет21) Тогда
		ОССчетЗабаланса=Истина;
	КонецЕсли;
	ПолныйРасчетДанных();
	Если ТблОтбор.Количество()=0 Тогда
		Сообщить("Данных для выгрузки не обнаружено. Обработка завершена!");
		Возврат;
	КонецЕсли;
	
	//возможность интерактивного выбора выгружаемых ОС
	Если ВклВыборОС=Истина Тогда
		Если ТблОтбор.Количество()>0 Тогда
			СписокОтбораОС=Новый СписокЗначений;
			Для Каждого СтрТЧ Из ТблОтбор Цикл
				ПредставлениеОС=СокрЛП(СтрТЧ.ИнвНомСсылка)+" - "+СокрЛП(СтрТЧ.ОССсылка);
				СписокОтбораОС.Добавить(СтрТЧ.ОССсылка,ПредставлениеОС,Истина);
			КонецЦикла;
			
			ДействиеОтметки=СписокОтбораОС.ОтметитьЭлементы("Отметьте выгружаемые ОС:");
			Если ДействиеОтметки=Истина Тогда
				Для НомСпискаВыбора=0 По (СписокОтбораОС.Количество()-1) Цикл
					ЭлементСписка=СписокОтбораОС.Получить(НомСпискаВыбора);
					Если ЭлементСписка.Пометка=Истина Тогда
						Продолжить;
					КонецЕсли;
					
					ИндексудалСтр=-1;
					Для Каждого СтрТЧ Из ТблОтбор Цикл
						ИндексудалСтр=ИндексудалСтр+1;
						Если СтрТЧ.ОССсылка=ЭлементСписка.Значение Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					ТблОтбор.Удалить(ИндексудалСтр);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	//заполнение таблицы для просмотра выгружаемых данных
	ТблРезультат.Очистить();
	ТблРезультат.Загрузить(ТблОтбор);
	
	//непосредственное формирование файла
	Сообщить("4 из 9. Формирование файла - INV.txt, "+СокрЛП(ТекущаяДата()));
	Для Каждого Отбор Из ТблОтбор Цикл
		//------------------------------------
		//запись ИНВЕНТАРНОЙ КАРТОЧКИ
		//------------------------------------
		
		//1 уникальный код записи
		//пропуск (заполнится потом)
		Стр1="|";
		
		//2 местоположение
		Местоположение=ИДПомПоУмолчанию;
		Попытка
			МестонахождениеОбъект=Отбор.ОССсылка.Местонахождение;
		Исключение
			МестонахождениеОбъект="";
		КонецПопытки;
		ВыгрузкаДляНЦУКС=Ложь;
		Попытка
			Если ТипЗнч(МестонахождениеОбъект)=Тип("СправочникСсылка.Местонахождение") Тогда
				ВыгрузкаДляНЦУКС=Истина;
			КонецЕсли;
		Исключение
		КонецПопытки;
		Если ВыгрузкаДляНЦУКС=Истина Тогда
			//выгрузка для НЦУКС
			ИДПомТекст=Прав(СокрЛП(МестонахождениеОбъект.Код),5);
			МестоположениеНаим=СокрЛП(МестонахождениеОбъект.Родитель.Наименование);
			МестоположениеНаим=МестоположениеНаим+?(НЕ МестоположениеНаим="",", ","")+СокрЛП(МестонахождениеОбъект.Наименование);
			Если НЕ МестоположениеНаим="" Тогда
				НайденСтр=зПОМ.Найти(ИДПомТекст,"УКЗ");
				Если НайденСтр = Неопределено Тогда
					НоваяСтрока=зПОМ.Добавить();
					НоваяСтрока.УКЗ=ИДПомТекст;
					НоваяСтрока.ПНаим=МестоположениеНаим;
					НоваяСтрока.Наим=МестоположениеНаим;
					Местоположение=ИДПомТекст;
				Иначе
					Местоположение=СокрЛП(НайденСтр.УКЗ);
				КонецЕсли;
			КонецЕсли;
		Иначе
			//выгрузка для обычной конфигурации
			МестоположениеНаим=СокрЛП(МестонахождениеОбъект);
			Если НЕ МестоположениеНаим="" Тогда
				НайденСтр=зПОМ.Найти(МестоположениеНаим,"ПНаим");
				Если НайденСтр = Неопределено Тогда
					ИДПом=ИДПом+1;
					ИДПомТекст=Формат(ИДПом,"ЧЦ=5; ЧДЦ=0; ЧВН=; ЧГ=0");
					НоваяСтрока=зПОМ.Добавить();
					НоваяСтрока.УКЗ=ИДПомТекст;
					НоваяСтрока.ПНаим=МестоположениеНаим;
					НоваяСтрока.Наим=МестоположениеНаим;
					Местоположение=ИДПомТекст;
				Иначе
					Местоположение=СокрЛП(НайденСтр.УКЗ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Стр1=Стр1+Местоположение+"|";
		
		//3 МОЛ
		Стр1=Стр1+СокрЛП(Отбор.МОЛСсылка.Код)+"|";
		НоваяСтрока=зМОЛ.Добавить();
		НоваяСтрока.УКЗ=Отбор.МОЛСсылка.Код;
		НоваяСтрока.ПНаим=Отбор.МОЛСсылка.Наименование;
		НоваяСтрока.Наим=БухгалтерскийУчет.ФИО(Отбор.МОЛСсылка.Сотрудник.Контрагент)+Прав(Строка(Отбор.МОЛСсылка.Код), 4);
		
		//4 счет
		СчётСбор=Строка(Отбор.ОССсылка.КБК)+".";
		СчётУКЗ=Строка(Отбор.ОССсылка.КБК.Код);
		Если Отбор.ОССсылка.КВД=Перечисления.КВД.Бюджет Тогда
			СчётСбор=СчётСбор+"1";
			СчётУКЗ=СчётУКЗ+"1";
			НаимСчёт="БЮДЖЕТ";
		ИначеЕсли Отбор.ОССсылка.КВД=Перечисления.КВД.Внебюджет Тогда
			СчётСбор=СчётСбор+"2";
			СчётУКЗ=СчётУКЗ+"2";
			НаимСчёт="ВНЕБЮДЖЕТ";
		Иначе
			СчётСбор=СчётСбор+"3";
			СчётУКЗ=СчётУКЗ+"3";
			НаимСчёт="ВРЕМЕННОЕРАСПОРЯЖЕНИЕ";
		КонецЕсли;
		СчётСбор=СчётСбор+"."+СокрЛП(Отбор.Счет);
		СчётУКЗ=СчётУКЗ+СокрЛП(Отбор.Счет);
		НаимСчёт=НаимСчёт+" "+СокрЛП(Отбор.Счет.Наименование)+" "+Строка(Отбор.ОССсылка.КБК);
		СчётУКЗ=СтрЗаменить(СчётУКЗ, ".","");
		СчётУКЗ=СтрЗаменить(СчётУКЗ, " ","");
		НайденСтр=зСЧТ.Найти(СчётУКЗ, "УКЗ");
		Если НайденСтр = Неопределено Тогда
			НоваяСтрока=зСЧТ.Добавить();
			НоваяСтрока.УКЗ=СчётУКЗ;
			НоваяСтрока.ПНаим=НаимСчёт;
			НоваяСтрока.НомСч=СчётСбор;
		КонецЕсли;
		Стр1=Стр1+СчётУКЗ+"|";
		
		//5 номенклатура
		ПНаим=?(СокрЛП(Отбор.ОССсылка.НаименованиеПолное)="",Отбор.ОССсылка.Наименование,Отбор.ОССсылка.НаименованиеПолное);
		Стр1=Стр1+ПолучитьКодНаименования(зНОМ,Отбор.ОССсылка,,ПНаим)+"|";
		
		//6 подразделение
		Стр1=Стр1+СокрЛП(Отбор.МОЛСсылка.Подразделение.Код)+"|";
		
		//7 группа
		Стр1=Стр1+"|";
		
		//8 инв.номер
		//пропуск (заполнится потом)
		Стр2="|";
		
		//9 номер карточки
		Стр2=Стр2+СокрЛП(Отбор.ОССсылка.НомерИнвКарт)+"|";
		
		//10 номер паспорта
		Стр2=Стр2+КонтрольДлиныПоля(Отбор.ОССсылка.НомерПаспорта, 30)+"|";
		
		//11 заводской номер
		ЗаводскойНомер=КонтрольДлиныПоля(Отбор.ОССсылка.ЗаводскойНомер, 30);
		Стр2=Стр2+ЗаводскойНомер+"|";
		
		//12 начальная стоимость
		КолОст="";
		КолОстЧ=0;
		Если ОССчетЗабаланса=Истина Тогда
			//для забаланса
			СуммаБаланса=СуммаБаланса+Отбор.Сумма;
			Сумма=Окр(Отбор.Сумма/Отбор.КолОст,2);
			КолОст="1.000";
			КолОстЧ=Отбор.КолОст;
		Иначе
			//для баланса
			Если (Отбор.ОССсылка.ГрупповойУчет=Истина) и (ВыгружатьГрКарточки=Истина) Тогда
				Сумма=Отбор.Сумма;
				КолОст=Формат(Отбор.КолОст,"ЧДЦ=3; ЧРД=.; ЧГ=0");
				КолОстЧ=Отбор.КолОст;
			Иначе
				Сумма=?(Отбор.КолОст=0,0,Окр(Отбор.Сумма/Отбор.КолОст,2));
				КолОст="1.000";
				КолОстЧ=?(Отбор.КолОст=0,1,Отбор.КолОст);
			КонецЕсли;
			СуммаБаланса=СуммаБаланса+Отбор.Сумма;
		КонецЕсли;
		Стр2=Стр2+Формат(Сумма, "ЧЦ=18; ЧДЦ=2; ЧРД=.; ЧГ=0")+"|";
		
		//13 дата ввода в эксплуатацию
		ДатаСостОс=Формат(Отбор.ДатаВводаВЭксплуатацию, "ДФ=dd.MM.yyyy");
		Стр2=Стр2+ДатаСостОс+"|";
		
		//14 количество
		Стр2=Стр2+КолОст+"|";
		
		//15 признак групповой карточки
		Если ОССчетЗабаланса=Истина Тогда
			//выгрузка групповых карточек для забаланса отсутствует
			Стр2=Стр2+"0|";
		Иначе
			Если (Отбор.ОССсылка.ГрупповойУчет=Истина) и (ВыгружатьГрКарточки=Истина) Тогда
				//запись первой позиции групповой карточки
				Стр2=Стр2+"2|";
			Иначе
				Стр2=Стр2+"0|";
			КонецЕсли;
		КонецЕсли;
		
		//16 фактическое место-положение
		Стр2=Стр2+Местоположение+"|";
		
		//17 фактическое количество + запись строки в инв. картотеку
		Стр2=Стр2+"1.000|";
		Для НН=1 По КолОстЧ Цикл
			СобратьСтрокуИНВ(тИНВ, тПСК, зНОМ, Отбор.ОССсылка, Отбор.МОЛСсылка, Стр1, Стр2, НН);
		КонецЦикла;
	КонецЦикла;
	
	//Формирование справочника помещений - POM.txt
	Сообщить("5 из 9. Формирование файла - POM.txt, "+СокрЛП(ТекущаяДата()));
	зПОМ.Свернуть("УКЗ, ПНаим, Наим");
	Для Каждого Строка Из зПОМ Цикл
		Стр=СокрЛП(Строка.УКЗ)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.ПНаим, 160)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.Наим, 30)+"|";
		тПОМ.ДобавитьСтроку(Стр);
	КонецЦикла;
	
	//Формирование справочника МОЛ - MOL.txt
	Сообщить("6 из 9. Формирование файла - MOL.txt, "+СокрЛП(ТекущаяДата()));
	зМОЛ.Свернуть("УКЗ, ПНаим, Наим");
	Для Каждого Строка Из зМОЛ Цикл
		Стр=СокрЛП(Строка.УКЗ)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.ПНаим, 160)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.Наим, 30)+"|";
		тМОЛ.ДобавитьСтроку(Стр);
	КонецЦикла;
	
	//Формирование справочника Номенклатуры - NOM.txt
	Сообщить("7 из 9. Формирование файла - NOM.txt, "+СокрЛП(ТекущаяДата()));
	зНОМ.Свернуть("УКЗ, ПНаим, Наим, ЕдИзм");
	Для Каждого Строка Из зНОМ Цикл
		Стр=СокрЛП(Строка.УКЗ)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.ПНаим, 160)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.Наим, 30)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.ЕдИзм, 30)+"|";
		тНОМ.ДобавитьСтроку(Стр);
	КонецЦикла;
	
	//Формирование справочника Счета - SCH.txt
	Сообщить("8 из 9. Формирование файла - SCH.txt, "+СокрЛП(ТекущаяДата()));
	зСЧТ.Свернуть("УКЗ, ПНаим, НомСч");
	Для Каждого Строка Из зСЧТ Цикл
		Стр=СокрЛП(Строка.УКЗ)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.ПНаим, 160)+"|";
		Стр=Стр+КонтрольДлиныПоля(Строка.НомСч, 30)+"|";
		тСЧТ.ДобавитьСтроку(Стр);
	КонецЦикла;
	
	//------------- Завершение работы ---------------------------------------
	Сообщить("9 из 9. Запись файлов выгрузки, "+СокрЛП(ТекущаяДата()));
	СоздатьКаталог(ПутьЗаписи);
	СоздатьКаталог(ПутьЗагрузки);
	тМОЛ.Записать(ПутьЗаписи+"mol.txt", "windows-1251");
	тНОМ.Записать(ПутьЗаписи+"nom.txt", "windows-1251");
	тПОМ.Записать(ПутьЗаписи+"pom.txt", "windows-1251");
	тПОД.Записать(ПутьЗаписи+"pod.txt", "windows-1251");
	тСЧТ.Записать(ПутьЗаписи+"sch.txt", "windows-1251");
	тИНВ.Записать(ПутьЗаписи+"inv.txt", "windows-1251");
	тПСК.Записать(ПутьЗаписи+"sos.txt", "windows-1251");
	тПГК.Записать(ПутьЗаписи+"grp.txt", "windows-1251");
	тМОД.Записать(ПутьЗаписи+"mod.txt", "windows-1251");
	
	ЭтотОбъект.СуммаКонтрол=СуммаБаланса;
	Сообщить("Выгрузка прошла удачно!, "+СокрЛП(ТекущаяДата()));
	Сообщить("Кол-во выгруженных карточек = "+СокрЛП(ТблОтбор.Количество()));
	
КонецПроцедуры
