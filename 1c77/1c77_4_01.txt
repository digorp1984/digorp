Процедура Сформировать()
	Если ПроверкаПериода() = 0 Тогда
		Возврат;
	КонецЕсли;    
	
    Расшифровка = СоздатьОбъект("СписокЗначений");
    Расшифровка.Установить("Отчет", "РасчетыСКонтрагентами2");
    Расшифровка.Установить("ВыбраннаяФирма", ВыбраннаяФирма);
	Расшифровка.Установить("НачДата", НачДата);
	Расшифровка.Установить("КонДата", КонДата);
    Расшифровка.Установить("КодВидаДеятельности", КодВидаДеятельности);
    Расшифровка.Установить("РазворотПоСчетам", РазворотПоСчетам);
    Расшифровка.Установить("РазворотСКБК", РазворотСКБК);
    Расшифровка.Установить("ИтогиПоГруппам", ИтогиПоГруппам);
    Расшифровка.Установить("РазворотПоДоговорам", РазворотПоДоговорам); 
    Расшифровка.Установить("СпКонтрагентов", СпКонтрагентов); 

	Если (ТипЗначенияСтр(Таб) = "Таблица") и
		 ((Обновить = 1) или (Обновить = 2)) Тогда
	
		Таб.Очистить();
	Иначе
	   	Таб = СоздатьОбъект("Таблица");
	КонецЕсли;
	Таб.ИсходнаяТаблица("Таблица");   
	
	Если Обновить = 2 Тогда
         СтрокаДействийФормы = "#Закрыть";
    КонецЕсли;

	СпСчетов = СоздатьОбъект("СписокЗначений");
	СобратьСписокСчетовДляЗапроса(СпСчетов);            
	
	Би = СоздатьОбъект("БухгалтерскиеИтоги");
	Если ПустоеЗначение(ВыбраннаяФирма) = 0 Тогда
		Би.ИспользоватьРазделительУчета(ВыбраннаяФирма);
	КонецЕсли;	                                      
	
	Если СпКонтрагентов.РазмерСписка() <> 0 Тогда
		Би.ИспользоватьСубконто(ВидыСубконто.Контрагенты,СпКонтрагентов,1);
	Иначе
		Би.ИспользоватьСубконто(ВидыСубконто.Контрагенты,,1);
	КонецЕсли;	                                         
	
	Если РазворотПоДоговорам = 1 Тогда
		Би.ИспользоватьСубконто(ВидыСубконто.Договоры,,1);
	КонецЕсли;	                                      
	
	Если РазворотСКБК = 1 Тогда
		Би.ИспользоватьСубконто(ВидыСубконто.ФКР,,1);
	КонецЕсли;	
	Би.ВыполнитьЗапрос(НачДата,КонДата,СпСчетов);

	НазвОрг = ""; 
	Если ПустоеЗначение(ВыбраннаяФирма) = 0 Тогда
		СписокДанных = СоздатьОбъект("СписокЗначений");
		СписокДанных.Установить( "Контекст", Контекст);
		СписокДанных.Установить( "Дата", КонДата);
		СписокДанных.Установить( "ОфициальноеНаименование","" ); 
		
		ГлПолучитьДанные(СписокДанных);
	
		НазвОрг = СписокДанных.Получить("ОфициальноеНаименование");
	КонецЕсли;     
	пКодВидаДеятельности = ?(ПустоеЗначение(КодВидаДеятельности) = 1,"По всем",КодВидаДеятельности);
	                              
	Таб.ВывестиСекцию("Секция_0");
	Таб.ВывестиСекцию("Шапка");

	Расшифровка.УдалитьВсе(); 
	
	Меню = СоздатьОбъект("СписокЗначений");
	Меню.ДобавитьЗначение("КарточкаСчета", "Карточка счета");
	Меню.ДобавитьЗначение("ПогасУсл", "Погасить услугу");
	Расшифровка.Установить("Меню", Меню);

	Если ПустоеЗначение(ВыбраннаяФирма)=1 Тогда
		Расшифровка.Установить("РазделительУчета",);
	Иначе
		Расшифровка.Установить("РазделительУчета", ВыбраннаяФирма);
	КонецЕсли;
	Расшифровка.Установить("Дата1", НачДата);
	Расшифровка.Установить("Дата2", КонДата);
	
	
	ВсегоНачОстД = 0;
	ВсегоНачОстК = 0;
	ВсегоОборотД = 0;
	ВсегоОборотК = 0;
	ВсегоКонОстД = 0;
	ВсегоКонОстК = 0;
	Би.ВыбратьСубконто(1);
	Пока Би.ПолучитьСубконто(1) = 1 Цикл
		ТекКонтрагент = Би.Субконто(1);  
		пОбъект       = ТекКонтрагент;

		Расшифровка.Установить("ВидСубконто3", Би.Счет.ВидСубконто(3));
		Расшифровка.Установить("ОтборСубконто3", 2);
		Расшифровка.Установить("Субконто3", ТекКонтрагент);
		Расшифровка.Установить("ВидСубконто4", Би.Счет.ВидСубконто(4));
		Расшифровка.Установить("ОтборСубконто4", 3);
		Расшифровка.Установить("Субконто4", "");
		
		
		ИтНачОстД  = Би.СНД("С");
		ИтНачОстК  = Би.СНК("С");
		ИтОборотД  = Би.ДО("С");
		ИтОборотК  = Би.КО("С");
		ИтКонОстД  = Би.СКД("С");
		ИтКонОстК  = Би.СКК("С");

		пНачОстД = ФорматС(ИтНачОстД);
		пНачОстК = ФорматС(ИтНачОстК);
		пОборотД = ФорматС(ИтОборотД);
		пОборотК = ФорматС(ИтОборотК);
		пКонОстД = ФорматС(ИтКонОстД);
		пКонОстК = ФорматС(ИтКонОстК);

		Таб.ВывестиСекцию("Строка1");     
		
		Если РазворотПоДоговорам = 1 Тогда
			РазвернутьПоДоговорам(Би);
		Иначе	
			Если РазворотПоСчетам = 1 Тогда
				РазвернутьПоСчетам(Би,2);
			КонецЕсли;	
		КонецЕсли;	

		ВсегоНачОстД = ВсегоНачОстД + ИтНачОстД;
		ВсегоНачОстК = ВсегоНачОстК + ИтНачОстК;
		ВсегоОборотД = ВсегоОборотД + ИтОборотД;
		ВсегоОборотК = ВсегоОборотК + ИтОборотК;
		ВсегоКонОстД = ВсегоКонОстД + ИтКонОстД;
		ВсегоКонОстК = ВсегоКонОстК + ИтКонОстК;
	КонецЦикла;	

	пНачОстД = ФорматС(ВсегоНачОстД);
	пНачОстК = ФорматС(ВсегоНачОстК);
	пОборотД = ФорматС(ВсегоОборотД);
	пОборотК = ФорматС(ВсегоОборотК);
	пКонОстД = ФорматС(ВсегоКонОстД);
	пКонОстК = ФорматС(ВсегоКонОстК);   
	
	ВсегоНачОстСв = ВсегоНачОстД - ВсегоНачОстК;
	Если ВсегоНачОстСв < 0 Тогда
		пНачОстКСв = ФорматС(-(ВсегоНачОстСв));
		пНачОстДСв = ФорматС(0);
	ИначеЕсли ВсегоНачОстСв > 0 Тогда
		пНачОстКСв = ФорматС(0);
		пНачОстДСв = ФорматС(ВсегоНачОстСв);
	Иначе	
		пНачОстКСв = ФорматС(0);
		пНачОстДСв = ФорматС(0);
	КонецЕсли;	
	ВсегоКонОстСв = ВсегоКонОстД - ВсегоКонОстК;
	Если ВсегоКонОстСв < 0 Тогда
		пКонОстКСв = ФорматС(-(ВсегоКонОстСв));
		пКонОстДСв = ФорматС(0);
	ИначеЕсли ВсегоКонОстСв > 0 Тогда
		пКонОстКСв = ФорматС(0);
		пКонОстДСв = ФорматС(ВсегоКонОстСв);
	Иначе	
		пКонОстКСв = ФорматС(0);
		пКонОстДСв = ФорматС(0);
	КонецЕсли;	

	Таб.ВывестиСекцию("Итог");     
	
	Таб.Опции(0,0,10,0); 
	Таб.ПовторятьПриПечатиСтроки(9,10);
	Таб.ОбластьПечати(3); 
	Таб.ПараметрыСтраницы(2,,,,,,,,,,1);
	Таб.ТолькоПросмотр(1);
	Таб.Показать("Расчеты с контрагентами");

КонецПроцедуры
